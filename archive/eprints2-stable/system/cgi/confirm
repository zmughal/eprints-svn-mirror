######################################################################
#
#  Confirm Password or Email Change
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use strict;

EPrints::Session::start;

my( $title, $page ) = make_confirm_page();
$page->appendChild( &SESSION->html_phrase( "general:frontpage_link" ) );

&SESSION->build_page( $title, $page, "confirm" );
&SESSION->send_page();
exit;

sub make_confirm_page
{
	my $page = &SESSION->make_doc_fragment;

	my $user_ds = &ARCHIVE->get_dataset( "user" );

	if( !&SESSION->have_parameters() )
	{
		$page->appendChild( &SESSION->html_phrase( "general:bad_param" ) );
		return( &SESSION->html_phrase( "cgi/confirm:err_title" ) , $page );
	}

	# Process the form.
	my $userid = &SESSION->param( "userid" )+0;
	my $pin = &SESSION->param( "pin" );

	my $user = new EPrints::User( $userid );

	if( !defined $user )
	{
		$page->appendChild( &SESSION->html_phrase( "cgi/confirm:bad_user" ) );
		return( &SESSION->html_phrase( "cgi/confirm:err_title" ) , $page );
	}

	my $userpin = $user->get_value( "pin" );
	my $pinsettime = $user->get_value( "pinsettime" );
	my $delta = (time - $pinsettime);

	if( !defined $userpin )
	{
		$page->appendChild( &SESSION->html_phrase( "cgi/confirm:no_pin" ) );
		return( &SESSION->html_phrase( "cgi/confirm:err_title" ) , $page );
	}
	if( $userpin ne $pin)
	{
		$page->appendChild( &SESSION->html_phrase( "cgi/confirm:pin_mismatch" ) );
		return( &SESSION->html_phrase( "cgi/confirm:err_title" ) , $page );
	}
	my $maxdelta = &ARCHIVE->get_conf( "pin_timeout" );
	if( ( $maxdelta != 0 ) && ( $maxdelta * 60 * 60 < $delta ) )
	{
		$page->appendChild( &SESSION->html_phrase( "cgi/confirm:pin_timeout" ) );
		return( &SESSION->html_phrase( "cgi/confirm:err_title" ) , $page );
	}

	# Only ONE of these should be set, as the two set_* scripts should zero the
	# other value when they set theirs.

	# This script hacks the SQL directly, as normally "secret" fields are not
	# accessable to eprints. 
	
	if( $user->is_set( "newemail" ) )
	{
		# check no one else has this email! cjg
		my $sql = "UPDATE ".$user_ds->get_sql_table_name()." SET email=newemail, newemail=NULL, pin=NULL where userid=".$userid; 
		&DATABASE->do( $sql );
		$page->appendChild( &SESSION->html_phrase( 
			"cgi/confirm:set_email",
			newemail=>&SESSION->make_text( $user->get_value( "newemail" ) ) ) );
	} 
	else
	{
		# Must be password then. Can't see it 'cus it's a "secret".
		my $sql = "UPDATE ".$user_ds->get_sql_table_name()." SET password=newpassword, newpassword=NULL, pin=NULL where userid=".$userid; 
		&DATABASE->do( $sql );
		$page->appendChild( &SESSION->html_phrase( "cgi/confirm:set_password" ) );
	}

	$page->appendChild( &SESSION->html_phrase( "cgi/confirm:username",
		username => $user->render_value( "username" ) ) );

	$page->appendChild( &SESSION->html_phrase( "cgi/confirm:go_login" ) );

	return( &SESSION->html_phrase( "cgi/confirm:title" ) , $page );
}

