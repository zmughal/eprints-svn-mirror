######################################################################
#
#  EPrint Editor
#
#   Allows staff to remove EPrints or transfer them back to the
#   submission buffer.
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::EPrint;
use EPrints::Session;

use strict;

#cjg check item is editable by this user.

#cjg remove does not work

EPrints::Session::start;

# Check we have privs
if( !&SESSION->auth_check( "editor" ) )
{
	exit( 0 );
}

my $datasetid = &SESSION->param( "dataset" );

# When in doubt, use the main archive.

$datasetid = "archive" if( !defined $datasetid );
if( $datasetid ne "archive" && 
    		$datasetid ne "inbox" && 
    		$datasetid ne "deletion" && 
    		$datasetid ne "buffer" )
{
	$datasetid = "archive";
}

my $dataset = &ARCHIVE->get_dataset( $datasetid );

my $eprintid = &SESSION->param( "eprintid" );

my $eprint = new EPrints::EPrint( $eprintid, $dataset );
my $action = &SESSION->get_action_button();

if( !defined $eprint )
{
	&SESSION->render_error( &SESSION->html_phrase(
		"cgi/users/edit_eprint:cant_find_it",
		id=>&SESSION->make_text( $eprintid ) ) );
	exit;
}

my $user_ds = &ARCHIVE->get_dataset( "user" );

my $can_edit = 0;
my $ef_field = $user_ds->get_field( 'editperms' );
my $searches = &SESSION->current_user->get_value( 'editperms' );
if( scalar @{$searches} == 0 )
{
	$can_edit = 1;
}

foreach my $s ( @{$searches} )
{
	next if( $can_edit ); # skip the rest if one matches

	my $search = $ef_field->make_searchexp( $s );
	if( $search->get_conditions->item_matches( $eprint ) )
	{
		$can_edit = 1;
	}
	$search->dispose;
}

unless( $can_edit )
{
	&SESSION->render_error( &SESSION->html_phrase(
		"cgi/users/edit_eprint:cant_edit",
		id=>&SESSION->make_text( $eprintid ) ) );
	exit;
}


if( !defined $action )
{
	&view_page( $eprint );
	exit;
}

if( $action eq "_toinbox" )
{
	# Bounce button pressed - get reason
	&bounce_form( 0, $eprint );
	exit;
}

if( $action eq "_remove" )
{
	# Remove button pressed - get reason
	&bounce_form( 1, $eprint );
	exit;
}

if( $action eq "_send" )
{
	# Actually do the bounce
	&bounce( $eprint );
	exit;
}

if( $action eq "_toarchive" )
{	
	# Accept button pressed

	my $dsid = $eprint->get_dataset->id();

	unless( $eprint->move_to_archive() )
	{
		&SESSION->render_error( &SESSION->html_phrase(
			"cgi/users/edit_eprint:cant_move",
			id=>&SESSION->make_text( $eprintid ) ) );
		exit;
	}

	# Successfully archived, redirect
	if( $dsid eq "buffer" )
	{
		&SESSION->redirect( "buffer" );
	}
	else
	{
		&SESSION->redirect( &ARCHIVE->get_conf( "userhome" ) );
	}
	exit;
}

if( $action eq "_tobuffer" )
{
	
	unless( $eprint->move_to_buffer() )
	{
		&SESSION->render_error( &SESSION->html_phrase(
			"cgi/users/edit_eprint:cant_move",
			id=>&SESSION->make_text( $eprintid ) ) );
		exit;
	}

	my $page = &SESSION->make_doc_fragment();
	$page->appendChild( &SESSION->html_phrase( 
		"cgi/users/edit_eprint:moved",
		link=>&SESSION->render_link( "edit_eprint?dataset=buffer&eprintid=".$eprintid ) ) );
	$page->appendChild( &SESSION->html_phrase( 
		"general:userhome_link" ) );
	&SESSION->build_page( &SESSION->html_phrase( "cgi/users/edit_eprint:move_title" ), $page, "move_eprint" );
	&SESSION->send_page();
	exit;
}

if( $action eq "_todeletion" )
{
	unless( $eprint->move_to_deletion() )
	{
		&SESSION->render_error( &SESSION->html_phrase(
			"cgi/users/edit_eprint:cant_remove",
			id=>&SESSION->make_text( $eprintid ) ) );
		exit;
	}
	my $page = &SESSION->make_doc_fragment();
	$page->appendChild( &SESSION->html_phrase( 
		"cgi/users/edit_eprint:removed" ) );
	$page->appendChild( &SESSION->html_phrase( 
		"general:userhome_link" ) );
	&SESSION->build_page( &SESSION->html_phrase( "cgi/users/edit_eprint:remove_title" ), $page, "move_removed" );
	&SESSION->send_page();
	exit;
}

if( $action eq "_clone" )
{
	my $new_eprint = $eprint->clone( &ARCHIVE->get_dataset( "buffer" ), 1 );

	unless( defined $new_eprint )
	{
		&SESSION->render_error( &SESSION->html_phrase(
			"cgi/users/edit_eprint:cant_clone",
			id=>&SESSION->make_text( $eprintid ) ) );
		exit;
	}
	
	&SESSION->redirect( "edit_eprint?dataset=buffer&eprintid=".$new_eprint->get_id() );
	exit;
}

# OK, so we are (presumably) editing it then...

my $stage = &SESSION->param( "stage" );
# If we are skipping the files stage then we have to work out what the
# actual last stage is...
my $laststage = "files";
my @stages = ( "meta","linking","type" );
while( &ARCHIVE->get_conf( "submission_stage_skip", $laststage ) )
{
	$laststage = pop @stages;
	last if( $laststage eq "type" );
}
my $ls = &ARCHIVE->get_conf( "submission_stage_last_for_staff_edit" );
$laststage = $ls if( defined $ls );

if( defined $stage && $laststage eq $stage && defined $action && ($action eq "finished" || $action eq "next") )
{
	# Intercept the verify page, that's what we were doing!
	&SESSION->redirect( "edit_eprint?dataset=$datasetid&eprintid=$eprintid" );
	exit;
}

# Give other cases to the edit form
my $subform = new EPrints::SubmissionForm(
	"edit_eprint?dataset=$datasetid&eprintid=$eprintid",
	1,
	$dataset,
	"edit_eprint" );

$subform->process();

if( $dataset->id eq "archive" || $dataset->id eq "deletion" )
{
	# If the eprint is in the main archive or deletion area
	# then we need to update its webpage. This will make editing
	# even slower, but editing the main db SHOULD be a rare thing
	# anyway.

	# get it from the DB again - it's probably changed.
	my $eprint = new EPrints::EPrint( $eprintid, $dataset );

	# update the static pages.
	$eprint->generate_static;
}

exit;


# Show metadata & options:

sub view_page
{
	my( $eprint ) = @_;

	my $page = &SESSION->make_doc_fragment();

	$page->appendChild( &SESSION->html_phrase( 
		"cgi/users/edit_eprint:status",
		dataset => &SESSION->html_phrase( "dataset_fieldopt_dataset_".$eprint->get_dataset()->id() ) ) );
	my( $p, $t ) = $eprint->render_full();
	$page->appendChild( $p );

	my $buttons = {};
	my $r1 = [];
	my $r2 = [];
	if( $eprint->get_dataset()->id() eq "inbox" )
	{
		$r1 = [ "_tobuffer" ];
		$r2 = [ "edit", "_remove", "_clone" ];
	}
	if( $eprint->get_dataset()->id() eq "buffer" )
	{
		$r1 = [];
		# only offer to return this to the inbox
		# if it's owned by a valid user.
		if( defined $eprint->get_user() )
		{
			push @{$r1}, "_toinbox";
		}
		push @{$r1}, "_toarchive";
		$r2 = [ "edit", "_remove", "_clone" ];
	}
	if( $eprint->get_dataset()->id() eq "archive" )
	{
		$r1 = [ "_tobuffer", "_todeletion" ];
		$r2 = [ "edit", "_clone" ];
	}
	if( $eprint->get_dataset()->id() eq "deletion" )
	{
		$r1 = [ "_toarchive" ];
		$r2 = [ "edit", "_clone" ];
	}
	
	my $form = &SESSION->render_form( "post", "edit_eprint" );
	foreach( @{$r1}, @{$r2} )
	{
		$buttons->{$_} = &SESSION->phrase( "cgi/users/edit_eprint:action_".$_ );
	}
	#print STDERR "**". $eprint->get_dataset()->id()."**\n";
	#print STDERR "**". join( '/', (@{$r1}, @{$r2}))."**\n";

	$form->appendChild( &SESSION->render_ruler() );
	$buttons->{_order} = $r1;
	$form->appendChild( &SESSION->render_action_buttons( %{$buttons} ) );

	$form->appendChild( &SESSION->render_ruler() );
	$buttons->{_order} = $r2;
	$form->appendChild( &SESSION->render_action_buttons( %{$buttons} ) );
	
	$form->appendChild( &SESSION->render_hidden_field( "eprintid", $eprint->get_id() ) );
	$form->appendChild( &SESSION->render_hidden_field( "dataset", $eprint->get_dataset()->id() ) );

	$page->appendChild( $form );
	$page->appendChild( &SESSION->html_phrase( "general:userhome_link" ) );
	
	&SESSION->build_page( &SESSION->html_phrase( "cgi/users/edit_eprint:form_title" ), $page, "move_form" );
	&SESSION->send_page();
}


sub bounce_form
{
	my( $delete, $eprint ) = @_;

	# Get the user's details
	my $user = $eprint->get_user();
	# We can't bounce it if there's no user associated - but
	# we can still delete it.
	if( !defined $user && !$delete)
	{
		&SESSION->render_error( 
			&SESSION->html_phrase( 
				"cgi/users/edit_eprint:no_user" ),
			"buffer" );
		return;
	}

	my $page = &SESSION->make_doc_fragment();

	my $form = &SESSION->render_form( "post", "edit_eprint" );
	if( defined $user )
	{
		$page->appendChild( &SESSION->html_phrase( 
				"cgi/users/edit_eprint:bounce_form_intro", 
				langpref=>$user->render_value( "lang" ) ) );
		my $div = &SESSION->make_element( 
				"div", 
				class => "formfieldinput" );
		my $textarea = &SESSION->make_element(
					"textarea",
					name => "reason",
					rows => 20,
					cols => 60,
					wrap => "virtual" );
		# remove any markup:
		my $title = &SESSION->make_text( 
			EPrints::Utils::tree_to_utf8( 
				$eprint->render_description() ) );

		$textarea->appendChild( &SESSION->html_phrase(
			($delete?"mail_delete_reason":"mail_bounce_reason"),
			title => $title ) );
		$div->appendChild( $textarea );
		$form->appendChild( $div );

		$form->appendChild( &SESSION->render_action_buttons(
			"_send" => &SESSION->phrase( 
				"cgi/users/edit_eprint:action_send" ) ) );
	}
	else
	{
		my $p = &SESSION->make_element( "p" );
		$p->appendChild( $eprint->render_description() );
		$form->appendChild( $p );
	
		$form->appendChild( &SESSION->render_action_buttons(
			"_send" => &SESSION->phrase( 
				"cgi/users/edit_eprint:action_reallydelete")));
	}


	$form->appendChild( &SESSION->render_hidden_field( 
				"eprintid", 
				$eprint->get_value( "eprintid" ) ) );
	$form->appendChild( &SESSION->render_hidden_field( 
				"dataset", 
				$eprint->get_dataset()->id() ) );
	$form->appendChild( &SESSION->render_hidden_field( 
				"delete", 
				$delete ) );

	$page->appendChild( $form );

	&SESSION->build_page(
		&SESSION->html_phrase( "cgi/users/edit_eprint:title_".( $delete ? "delete" : "bounce" )."_form" ),
		$page,
		"editeprint_form" );
	&SESSION->send_page();
}


sub bounce
{
	my( $eprint ) = @_;

	my $delete = &SESSION->param( "delete" );

	# Get the user's details
	my $user = $eprint->get_user();
	# We can't bounce it if there's no user associated - but
	# we can still delete it.
	if( !defined $user && !$delete)
	{
		# Can't find the user
		&SESSION->render_error( 
			&SESSION->html_phrase( 
				"cgi/users/edit_eprint:no_user" ),
			"buffer" );
		return;
	}

	my $success = 0;

	if( $delete )
	{
		# Delete the submission
		$success = $eprint->remove();
	}
	else
	{
		# Transfer the EPrint back to the user's inbox
		$success = $eprint->move_to_inbox();
	}

	unless( $success )
	{
		# Couldn't be bounced at all
		&SESSION->render_error( 
			&SESSION->html_phrase( 
				"cgi/users/edit_eprint:bord_fail" ),
			"buffer" );
		return;
	}

	if( !defined $user )
	{
		# Can't send mail to a user what does not
		# exist.
		&SESSION->redirect( "buffer" );
		return;
	}
	
	my $mail = &SESSION->make_element( "mail" );
	$mail->appendChild( &SESSION->make_text( 
				&SESSION->param( "reason" ) ) );

	# Successfully transferred, mail the user with the reason
	if( $user->mail(
		"cgi/users/edit_eprint:subject_bounce",
		$mail,
		&SESSION->current_user() ) )
	{	
		# Successfully bounced, redirect
		&SESSION->redirect( "buffer" );
		return;
	}

	# Couldn't mail
	&SESSION->render_error( 
		&SESSION->html_phrase( "cgi/users/edit_eprint:mail_fail",
			username=>$user->render_value( "username" ),
			email=>$user->render_value( "email" ) ),
		"buffer" );
}
