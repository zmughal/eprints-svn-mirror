######################################################################
#
#  Show EPrints modified or added in the past 7 days
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use EPrints::EPrint;
use EPrints::SearchExpression;

use strict;
EPrints::Session::start;

my $ds = &ARCHIVE->get_dataset( "archive" );
my $page = &SESSION->make_doc_fragment();

my $citation = &ARCHIVE->get_conf( "latest_citation" );

my $seensome = 0;
for( my $d=0; $d<7; ++$d )
{
	my $t = time-60*60*24*$d;
	my $searchexp = new EPrints::SearchExpression( dataset => $ds );
	

	my $date = EPrints::Utils::get_datestamp( $t );
	$searchexp->add_field( $ds->get_field( "datestamp" ), $date );
		
	$searchexp->perform_search();

	if( $searchexp->count() )
	{
		$seensome = 1;

		my $day;
		if( $d == 0 )
		{
			$day = &SESSION->html_phrase( "cgi/latest:today" );
		}
		elsif( $d == 1 )
		{
			$day = &SESSION->html_phrase( "cgi/latest:yesterday" );
		}
		else
		{
			my $dow = (localtime($t))[6];
			$day = &SESSION->html_phrase( "cgi/latest:day_".$dow );
		}

		my $h2= &SESSION->make_element( "h2" );
		$h2->appendChild( $day );
		$page->appendChild( $h2 );
	
		$searchexp->map( sub{ 
			my( $dataset, $item, $info ) = @_;
		
			my $p = &SESSION->make_element( "p" );
			$p->appendChild(
				 $item->render_citation_link( $citation ) );
			$page->appendChild( $p );
		} );
	
		$page->appendChild( &SESSION->render_ruler() );
	}
	$searchexp->dispose();
}
unless( $seensome )
{
	$page->appendChild( &SESSION->html_phrase( "cgi/latest:none" ) );
}

$page->appendChild( &SESSION->html_phrase( "general:frontpage_link" ) );
	
my $title = &SESSION->html_phrase( "cgi/latest:title" );

&SESSION->build_page( $title, $page, "latest" );
&SESSION->send_page();

exit;


