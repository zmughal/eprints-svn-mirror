######################################################################
#
#  Show Last "n" EPrints added.
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use EPrints::EPrint;
use EPrints::SearchExpression;

use strict;

EPrints::Session::start;

my $ds = &ARCHIVE->get_dataset( "archive" );
my $page = &SESSION->make_doc_fragment();

my $citation = &ARCHIVE->get_conf( "latest_tool_citation" );
my $max = 20;
my $mode = &SESSION->param( "mode" );
$mode = "default" if( !defined $mode );
my $filters = [];
my $conf = &ARCHIVE->get_conf( "latest_tool_modes", $mode );
my $class= "latest_tool";
if( defined $conf )
{
	foreach my $key (keys %{$conf} )
	{
		$citation = $conf->{"citation"} if( $key eq "citation" );
		$filters = $conf->{"filters"} if( $key eq "filters" );
		$max = $conf->{"max"} if( $key eq "max" );
	}
	$class.= "_".$mode;
}
my $number_to_get = $max;
my $n = &SESSION->param( "n" );
if( $n > 0 && $n <= $max )
{
	$number_to_get = $n;
}
my $searchexp = new EPrints::SearchExpression(
	allow_blank => 1,
	filters => $filters,
	custom_order => "-datestamp",
	dataset => $ds );
my $title;
if( EPrints::Utils::is_set( $filters ) )
{
	$title = &SESSION->html_phrase( 
		"cgi/latest_tool:title_matching",
		n => &SESSION->make_text( $number_to_get ),
		search => $searchexp->render_description );
}
else
{
	$title = &SESSION->html_phrase( 
		"cgi/latest_tool:title",
		n => &SESSION->make_text( $number_to_get ) );
}

$searchexp->perform_search();
my @records = $searchexp->get_records( 0, $number_to_get );
$searchexp->dispose();

my $output = &SESSION->param( "output" );
if( $output eq "rss" )
{
	my $response = &SESSION->make_element( "rdf:RDF",
		"xmlns:rdf"=>"http://www.w3.org/1999/02/22-rdf-syntax-ns#",
		"xmlns"=>"http://purl.org/rss/1.0/" );

	my $channel = &SESSION->make_element( "channel",
		"rdf:about"=>"http://www.iamcal.com/rss.php" );
	$response->appendChild( $channel );

	$channel->appendChild( &SESSION->render_data_element(
		4,
		"title",
		EPrints::Session::best_language( 
			&SESSION->get_langid(),
			%{&ARCHIVE->get_conf( "archivename" )} ) ) );

	$channel->appendChild( &SESSION->render_data_element(
		4,
		"link",
		&ARCHIVE->get_conf( "frontpage" ) ) );

	$channel->appendChild( &SESSION->render_data_element(
		4,
		"description", 
		&ARCHIVE->get_conf( "oai","content","text" ) ) );

	$channel->appendChild( &SESSION->render_data_element(
		4,
		"pubDate", 
		EPrints::Utils::get_timestamp() ) );

	$channel->appendChild( &SESSION->render_data_element(
		4,
		"lastBuildDate", 
		EPrints::Utils::get_timestamp() ) );

	$channel->appendChild( &SESSION->render_data_element(
		4,
		"language", 
		&SESSION->get_langid ) );

	$channel->appendChild( &SESSION->render_data_element(
		4,
		"copyright", 
		"" ) );

	$channel->appendChild( &SESSION->render_data_element(
		4,
		"docs", 
		"" ) );

#	<title>Footle</title>
#	<link>http://www.foo.com/</link>
#	<description>The Foo.</description>
#	<lastBuildDate>Tuesday, November 4, 2003 4:31:05 PM</lastBuildDate>
#	<pubDate>Tuesday, November 4, 2003 4:31:05 PM</pubDate>
#	<language>en</language>
#	<copyright>(C)2003 Footle</copyright>
#	<docs>http://www.foo.com/rss.html</docs>

	my $items = &SESSION->make_element( "items" );
	$channel->appendChild( $items );
	my $seq = &SESSION->make_element( "rdf:Seq" );
	$items->appendChild( $seq );

	foreach my $eprint ( @records )
	{
		my $li = &SESSION->make_element( "rdf:li",
			"rdf:resource"=>$eprint->get_url );
		$seq->appendChild( $li );

		my $item = &SESSION->make_element( "item",
			"rdf:about"=>$eprint->get_url );

		$item->appendChild( &SESSION->render_data_element(
			2,
			"title",
			EPrints::Utils::tree_to_utf8( $eprint->render_description ) ) );
		$item->appendChild( &SESSION->render_data_element(
			2,
			"link",
			$eprint->get_url ) );
		$item->appendChild( &SESSION->render_data_element(
			2,
			"description",
			EPrints::Utils::tree_to_utf8( $eprint->render_citation ) ) );
		$response->appendChild( $item );		
	}	

	my $content = "text/xml";
	&SESSION->send_http_header( content_type=>$content );

	print <<END;
<?xml version="1.0" encoding="utf-8" ?>

END
	print EPrints::XML::to_string( $response );
	EPrints::XML::dispose( $response );
	print "\n\n";
	
}
else
{

	foreach my $item ( @records )
	{
		my $p = &SESSION->make_element( "p", class=>$class );
		$p->appendChild( $item->render_citation_link( $citation ) );
		$page->appendChild( $p )
	}
	&SESSION->build_page( $title, $page, "latest_tool" );
	&SESSION->send_page();
}

exit;


