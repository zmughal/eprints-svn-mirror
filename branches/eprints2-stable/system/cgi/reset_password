######################################################################
#
#  EPrints Set Password 
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use strict;

EPrints::Session::start;

my( $page, $title ) = make_page();

# $page->appendChild( &SESSION->html_phrase( "general:frontpage_link" ) );

&SESSION->build_page( $title, $page, "reset_password" );
&SESSION->send_page();

exit;

sub make_page
{
	my $page = &SESSION->make_doc_fragment;

	my $title = &SESSION->html_phrase( "cgi/set_password:password_changeonlytitle" );

	my $user_ds = &ARCHIVE->get_dataset( "user" );

	my $f_email = $user_ds->get_field( "email" )->clone();
	$f_email->set_property( "confid" , "setpass" );

	my $f_newpass = $user_ds->get_field( "newpassword" )->clone();
	$f_newpass->set_property( "confid" , "setpass" );

	if( ! &SESSION->have_parameters() )
	{
		$page->appendChild( &SESSION->html_phrase( "cgi/set_password:pchange" ) );

		my $fields = [ $f_email, $f_newpass ];

		$page->appendChild( &SESSION->render_input_form(
			fields=>$fields,
			values=>{
				lang => &SESSION->get_langid()
			},
			show_help=>1,
			default_action=>"submit",
			buttons=>{
				submit=>&SESSION->phrase( "cgi/set_password:action_submit" )
			},
			dest=>"reset_password" ) );

		return( $page, $title );
	}

	# Process the form.
	my $email = $f_email->form_value;
	my $lang = &SESSION->get_langid();
	my $newpassword = $f_newpass->form_value;

	my $user = EPrints::User::user_with_email( $email );

	if( ! EPrints::Utils::is_set( $email ) )
	{
		$page->appendChild( &SESSION->html_phrase( 
			"cgi/set_password:no_email" ) );
		return( 
			$page, 
			&SESSION->html_phrase( 
				"cgi/set_password:error_title" ) );
	}

	if( !defined $user )
	{
		$page->appendChild( &SESSION->html_phrase( 
			"cgi/set_password:no_such_user",
			email=>&SESSION->make_text( $email ) ) );
		return( 
			$page, 
			&SESSION->html_phrase( 
				"cgi/set_password:error_title" ) );
	}

	if( !$user->has_priv( "set-password" ) )
	{
		$page->appendChild( &SESSION->html_phrase( 
			"cgi/set_password:no_priv",
			email=>&SESSION->make_text( $email ) ) );
		return( 
			$page, 
			&SESSION->html_phrase( 
				"cgi/set_password:error_title" ) );
	}

	if( !defined $newpassword || $newpassword eq "" )
	{
		$page->appendChild( &SESSION->html_phrase( 
			"cgi/set_password:no_password" ) );
		return( 
			$page, 
			&SESSION->html_phrase( 
				"cgi/set_password:error_title" ) );
	}

	$user->set_value( "newpassword", $newpassword );
	my $pin = sprintf( "%04X%04X%04X%04X",int rand 0xffff,int rand 0xffff,int rand 0xffff,int rand 0xffff );
	$user->set_value( "newemail", undef );
	$user->set_value( "pin", $pin );
	$user->set_value( "pinsettime", time() );
	$user->commit();
	$user->mail( 
		"cgi/set_password:account",
		&SESSION->html_phrase( 
			"mail_password_pin", 
			confirmurl => &SESSION->make_text( &ARCHIVE->get_conf( "perl_url" )."/confirm?userid=".$user->get_value( "userid" )."&pin=".$pin ),
			username => $user->render_value( "username" ) ) );
	$page->appendChild( &SESSION->html_phrase( 
		"cgi/set_password:mail_sent",
		email=>&SESSION->make_text( $email ) ) );
	return( $page, $title );
}

