######################################################################
#
#  EPrints Author Home
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::User;
use EPrints::Database;
use EPrints::Session;
use EPrints::EPrint;

use strict;


######################################################################
#
# Session initialisation
#
######################################################################

EPrints::Session::start;

# Any user can view this page, but most BE a user.
if( !&SESSION->auth_check )
{
	exit( 0 );
}

# If we were sent here by change_user then we have an icky url
# eg .../home/username rather than .../home 
# let's clean it up...
if( $ENV{PATH_INFO} ne "" )
{
	&SESSION->redirect( &ARCHIVE->get_conf( "userhome" ) );
	exit;
}
	
my $user = &SESSION->current_user;

my $fullname = $user->render_description();

my $probs = $user->validate();
my $valid_user = ( scalar @{$probs} == 0 );

# If not a valid user then redirect them to the user-info page.
# This'll give us info like "name".
unless( $valid_user )
{	
	&SESSION->redirect( "record" );
	exit;
}

######################################################################
#
# Render page
#
######################################################################

my( $page, $dt, $dd, $a );
my( $table, $tr, $td );



my $changeuserpath = "/change_user/".($user->get_value( "username" ));
my $securehost = &ARCHIVE->get_conf( "securehost" );
my $securepath = &ARCHIVE->get_conf( "securepath" );
if( EPrints::Utils::is_set( $securehost ) )
{
	$changeuserpath = $securepath.$changeuserpath;
}

my $tools = [
	### Create New Item
	{ 
		location=>"create",
		code=>"new_item",
		page=>"submit?_action_new=1",
		priv=>"deposit"
	},
	### Change User Info
	{ 
		location=>"user",
		code=>"change",
		page=>"record",
		notpriv=>"no_edit_own_record"
	},
	### Change subscription options
	{ 
		location=>"user",
		code=>"subscribe",
		page=>"subscribe",
		priv=>"subscription"
	},
	### View the archive status page
	{ 
		location=>"staff",
		code=>"status",
		page=>"status",
		priv=>"view-status"
	},
	### Review items we've got in archive
	{
		location=>"review",
		code=>"review",
		page=>"review",
		priv=>"deposit"
	},
	### View the Editor submission buffer
	{
		location=>"staff",
		code=>"staff_buffer",
		page=>"staff/buffer",
		priv=>"editor"
	},
	### Staff EPrints Search
	{
		location=>"staff",
		code=>"staff_eprint_search",
		page=>"search",
		priv=>"staff-view"
	},
	### Staff User Search
	{
		location=>"staff",
		code=>"staff_user_search",
		page=>"search/users",
		priv=>"staff-view"
	},
	### Add New User
	{
		location=>"staff",
		code=>"staff_add_user",
		page=>"staff/add_user",
		priv=>"edit-user"
	},
	### Change Email
	{
		location=>"user",
		code=>"change_email",
		page=>"change_email",
		priv=>"change-email"
	},
	### Use the Subject Edit Tool
	{
		location=>"staff",
		code=>"staff_edit_subject",
		page=>"staff/edit_subject",
		priv=>"edit-subject"
	},
	### Change the current login user
	{
		location=>"user",
		code=>"change_user",
		page=>$changeuserpath,
		priv=>"change-user"
	}
];




my $dls = {};
my $tool;
foreach	$tool (@{$tools})
{
	if( defined $tool->{priv} )
	{
		next unless $user->has_priv( $tool->{priv} );
	}
	if( defined $tool->{invpriv} )
	{
		next if $user->has_priv( $tool->{invpriv} );
	}
	if( !defined $dls->{$tool->{location}} )
	{
		$dls->{$tool->{location}} = &SESSION->make_element( "dl" );
	}
	my $dl = $dls->{$tool->{location}};
	$dt = &SESSION->make_element( "dt" );
	$a = &SESSION->render_link( $tool->{page} );
	$a->appendChild( &SESSION->html_phrase( "cgi/users/home:".$tool->{code}."_link" ) );
	$dt->appendChild( $a );
	$dl->appendChild( $dt );

	$dd = &SESSION->make_element( "dd" );
	$dd->appendChild( &SESSION->html_phrase( "cgi/users/home:".$tool->{code}."_info" ) );
	$dl->appendChild( $dd );
}








$page = &SESSION->make_doc_fragment();

### Welcome author blurb
$page->appendChild( &SESSION->html_phrase( "cgi/users/home:intro" ) );

$table = &SESSION->make_element("table");
$tr = &SESSION->make_element("tr");
$table->appendChild( $tr );
$page->appendChild( $table );

if( $user->has_priv( "deposit" ) )
{
	$td = &SESSION->make_element("td",valign=>"top");
	$tr->appendChild( $td );

	my $div;
	### Workspace
	$div = &SESSION->make_element("div",class=>"box");
	$div->appendChild( &render_workspace( $user, $dls->{create} ) );
	$td->appendChild( $div );
	

	### Pending
	$div = &SESSION->make_element("div",class=>"box");
	$div->appendChild( &render_pending( $user ) );
	$td->appendChild( $div );

	### Recent Additions
	$div = &SESSION->make_element("div",class=>"box");
	$div->appendChild( &render_recent( $user, 10, $dls->{review} ) );
	$td->appendChild( $div );
}

if( defined $dls->{user} )
{
	$td = &SESSION->make_element("td",valign=>"top", style=>"padding-left: 3em;");
	$tr->appendChild( $td );
	$td->appendChild( $dls->{user} );
}


if( defined $dls->{staff} )
{
	$page->appendChild( &SESSION->render_ruler );
	$page->appendChild( &SESSION->html_phrase( "cgi/users/home:staff_options" ) );
	$page->appendChild( $dls->{staff} );
}



&SESSION->build_page( 
		&SESSION->html_phrase( 
			"cgi/users/home:user_home", 
			name => $fullname ),
		$page,
		"user_home" );
&SESSION->send_page();

exit;



######################################################################
#
# render_workspace( $user )
#
#  This puts all of the user's items in the inbox table in a form.
#  The buttons will POST stuff to the submit script, with the possible
#  actions:
#
#   New     (Create a new EPrint)
#   Edit    (Edit a selected EPrint)
#   Clone   (Clone the selected EPrint)
#   Delete  (Delete the selected EPrint)
#   Deposit (Submit the selected EPrint to the archive)
#
#
#  If the user has no items in the inbox, a single button is drawn,
#  Allowing them to start the upload process.
#
######################################################################

sub render_workspace
{
	my( $user, $add ) = @_;

	### Get the actual items in the workspace
	my $ds = &ARCHIVE->get_dataset( "inbox" );
	my @eprints = $user->get_owned_eprints( $ds );

	my( $html, $form, $div );

	$html = &SESSION->make_doc_fragment();
	$html->appendChild( &SESSION->html_phrase( "cgi/users/home:docs" ));
	if( defined $add )
	{
		$html->appendChild( $add );
	}

	if( scalar @eprints == 0)
	{
		# No items, oh well.
		return $html;
	}

	# One or more items in workspace.
	# Render a form to let the user fiddle with them.

	my( $table, $tr, $td );

	$table = &SESSION->make_element( "table",
			cellpadding=>3,
			cellspacing=>0,
			border=>0 );
	my $e;
	my $cnt = 0;
	my $row = 0;
	foreach $e (@eprints)
	{
		$tr = &SESSION->make_element( "tr", class=>"row_".($row?"b":"a") );
		$table->appendChild( $tr );	
		$td = &SESSION->make_element( "td" );
		$tr->appendChild( $td );
		$td->appendChild( $e->render_description() );
		$td = &SESSION->make_element( "td" );
		$tr->appendChild( $td );
	#	$td->appendChild( &SESSION->html_phrase( 
	#		"cgi/users/home:accepted_at",
	#		time=>$e->render_value( "datestamp" ) ) );
		my $a;

		$a = &SESSION->render_link( "submit?eprintid=".$e->get_id."&_action_edit=1" );
		$a->appendChild( &SESSION->html_phrase("cgi/users/home:action_edit") );
		$td->appendChild( $a );

		$td->appendChild( &SESSION->make_text( ", " ) );

		$a = &SESSION->render_link( "submit?eprintid=".$e->get_id."&_action_submit=1" );
		$a->appendChild( &SESSION->html_phrase("cgi/users/home:action_submit") );
		$td->appendChild( $a );

		$td->appendChild( &SESSION->make_text( ", " ) );

		$a = &SESSION->render_link( "submit?eprintid=".$e->get_id."&_action_clone=1" );
		$a->appendChild( &SESSION->html_phrase("cgi/users/home:action_clone") );
		$td->appendChild( $a );

		$td->appendChild( &SESSION->make_text( ", " ) );

		$a = &SESSION->render_link( "submit?eprintid=".$e->get_id."&_action_delete=1" );
		$a->appendChild( &SESSION->html_phrase("cgi/users/home:action_delete") );
		$td->appendChild( $a );
		$row=!$row;
	}

	$html->appendChild( &SESSION->html_phrase( "cgi/users/home:depositing" ) );
	$html->appendChild( $table );

	return $html;
}



######################################################################
#
# render_pending( $user)
#
#  Render any items that are in the submissions buffer, if any
#
######################################################################

sub render_pending
{
	my( $user ) = @_;

	### Get the items in the buffer
	my $ds = &ARCHIVE->get_dataset( "buffer" );
	my @eprints =  $user->get_owned_eprints( $ds );

	my $html = &SESSION->make_doc_fragment();

	$html->appendChild( &SESSION->html_phrase( "cgi/users/home:pending_title" ) );

	if( scalar @eprints == 0 )
	{
		$html->appendChild( &SESSION->html_phrase( "cgi/users/home:no_pending" ) );
	}
	else
	{
		my( $table, $tr, $td );

		$table = &SESSION->make_element( "table",
				cellpadding=>3,
				cellspacing=>0,
				border=>0 );
		$html->appendChild( $table );
		my $row = 0;
		foreach my $e (@eprints)
		{
			$tr = &SESSION->make_element( "tr", class=>"row_".($row?"b":"a") );
			$table->appendChild( $tr );	
			$td = &SESSION->make_element( "td" );
			$tr->appendChild( $td );
			$td->appendChild( $e->render_description() );
			$td = &SESSION->make_element( "td" );
			$tr->appendChild( $td );
			$td->appendChild( &SESSION->html_phrase( 
				"cgi/users/home:deposited_at",
				time=>$e->render_value( "datestamp" ) ) );
			$row=!$row;
		}
	}
	return $html;
}
	

######################################################################
#
# render_recent( $user, $max, $review )
#
#  Render any items that have been recently accepted into the main 
#  archive.
#
######################################################################

sub render_recent
{
	my( $user, $max, $review ) = @_;
	
	### Get the items in the buffer
	my $ds = &ARCHIVE->get_dataset( "archive" );
	my @eprints =  $user->get_owned_eprints( $ds );

	my $html = &SESSION->make_doc_fragment();

	$html->appendChild( &SESSION->html_phrase( "cgi/users/home:recent_title" ) );

	if( scalar @eprints == 0 )
	{
		$html->appendChild( &SESSION->html_phrase( "cgi/users/home:no_recent" ) );
	}
	else
	{
		my( $table, $tr, $td );
	
		$table = &SESSION->make_element( "table",
				cellpadding=>3,
				cellspacing=>0,
				border=>0 );
		$html->appendChild( $table );
		my $e;
		my $cnt = 0;
		my $row = 0;
		foreach $e (reverse @eprints) 
#cjg not exactly the right way!
#cjg should really do it by deposit date
		{
			last if( $cnt++ == $max );
			$tr = &SESSION->make_element( "tr", class=>"row_".($row?"b":"a") );
			$table->appendChild( $tr );	
			$td = &SESSION->make_element( "td" );
			$tr->appendChild( $td );
		        my $a = &SESSION->render_link( $e->get_url() );
			$a->appendChild( $e->render_description() );
			$td->appendChild( $a );
			$td = &SESSION->make_element( "td" );
			$tr->appendChild( $td );
			$td->appendChild( &SESSION->html_phrase( 
				"cgi/users/home:accepted_at",
				time=>$e->render_value( "datestamp" ) ) );
			$row=!$row;
		}
	}

	if( defined $review )
	{
		$html->appendChild( $review );
	}
	return $html;
}

