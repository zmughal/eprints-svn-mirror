######################################################################
#
#  EPrints Staff Status Page
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use EPrints::Database;
use EPrints::EPrint;
use EPrints::User;

use strict;

EPrints::Session::start;

# Number of users in each group
my $total_users = &ARCHIVE->get_dataset( "user" )->count;

my( %num_users, $usertypes, $usertype, $userds, $subds );
$userds = &ARCHIVE->get_dataset( "user" );
$subds = &ARCHIVE->get_dataset( "subscription" );
$usertypes = $userds->get_types();
foreach $usertype ( @{$usertypes} )
{
	my $searchexp = new EPrints::SearchExpression( dataset => $userds );

	$searchexp->add_field(
		$userds->get_field( "usertype" ),
		$usertype );

	$searchexp->perform_search();
	$num_users{ $usertype } = $searchexp->count();
	$searchexp->dispose();
}

my %num_eprints = ();
my @esets = ( "archive", "buffer", "inbox", "deletion" );

foreach( @esets )
{
	# Number of submissions in dataset
	$num_eprints{$_} = &ARCHIVE->get_dataset( $_ )->count;
}

my $db_status = ( $total_users > 0 ? "OK" : "DOWN" );

my( $html , $table , $p , $span );

# Write the results to a table

$html = &SESSION->make_doc_fragment;

$table = &SESSION->make_element( "table", border=>"0" );
$html->appendChild( $table );

$table->appendChild( render_row( 
		&SESSION->html_phrase( "cgi/users/status:release" ),
		&SESSION->make_text( 
			EPrints::Config::get( "version" ) ) ) );

$table->appendChild(
	render_row( 
		&SESSION->html_phrase( "cgi/users/status:database" ),
		&SESSION->make_text( $db_status ) ) );

$table = &SESSION->make_element( "table", border=>"0" );
$html->appendChild( &SESSION->html_phrase( "cgi/users/status:usertitle" ) );
$html->appendChild( $table );

foreach $usertype ( keys %num_users )
{
	$table->appendChild(
		render_row( 
			&SESSION->make_text(
				$userds->get_type_name( $usertype ).":"), 
			&SESSION->make_text( $num_users{$usertype} ) ) );
}
$table->appendChild(
	render_row( 
		&SESSION->html_phrase( "cgi/users/status:users" ),
		&SESSION->make_text( $total_users ) ) );

$table = &SESSION->make_element( "table", border=>"0" );
$html->appendChild( &SESSION->html_phrase( "cgi/users/status:articles" ) );
$html->appendChild( $table );

foreach( @esets )
{
	$table->appendChild(
		render_row( 
			&SESSION->html_phrase( "cgi/users/status:set_".$_ ),
			&SESSION->make_text( $num_eprints{$_} ) ) );
}


unless( $EPrints::SystemSettings::conf->{disable_df} )
{
	$table = &SESSION->make_element( "table", border=>"0" );
	$html->appendChild( &SESSION->html_phrase( "cgi/users/status:diskspace" ) );
	$html->appendChild( $table );

	my $best_size = 0;

	my @dirs = &ARCHIVE->get_store_dirs();
	my $dir;
	foreach $dir ( @dirs )
	{
		my $size = &ARCHIVE->get_store_dir_size( $dir );
		$table->appendChild(
			render_row( 
				&SESSION->html_phrase( 
					"cgi/users/status:diskfree",
					dir=>&SESSION->make_text( $dir ) ),
				&SESSION->html_phrase( 
					"cgi/users/status:mbfree",
					mb=>&SESSION->make_text( 
						int($size/1024) ) ) ) );
	
		$best_size = $size if( $size > $best_size );
	}
	
	if( $best_size < &ARCHIVE->get_conf( "diskspace_error_threshold" ) )
	{
		$p = &SESSION->make_element( "p" );
		$html->appendChild( $p );
		$p->appendChild( 
			&SESSION->html_phrase( 
				"cgi/users/status:out_of_space" ) );
	}
	elsif( $best_size < &ARCHIVE->get_conf( "diskspace_warn_threshold" ) )
	{
		$p = &SESSION->make_element( "p" );
		$html->appendChild( $p );
		$p->appendChild( 
			&SESSION->html_phrase( 
				"cgi/users/status:nearly_out_of_space" ) );
	}
}


$table = &SESSION->make_element( "table", border=>"0" );
$html->appendChild( &SESSION->html_phrase( "cgi/users/status:subscriptions" ) );
$html->appendChild( $table );

$table->appendChild(
	render_row( 
		&SESSION->make_doc_fragment,
		&SESSION->html_phrase( "cgi/users/status:subcount" ),
		&SESSION->html_phrase( "cgi/users/status:subsent" ) ) );
foreach my $freq ( "never", "daily", "weekly", "monthly" )
{
	my $sent;
	if( $freq ne "never" )
	{
		$sent = EPrints::Subscription::get_last_timestamp( $freq );
	}
	if( !defined $sent )
	{
		$sent = "?";
	}
	my $searchexp = new EPrints::SearchExpression( dataset => $subds );

	$searchexp->add_field(
		$userds->get_field( "frequency" ),
		$freq );

	$searchexp->perform_search();
	my $n = $searchexp->count;
	$searchexp->dispose;

	$table->appendChild(
		render_row( 
			&SESSION->html_phrase( 
				"subscription_fieldopt_frequency_".$freq ),
			&SESSION->make_text( $n ),
			&SESSION->make_text( $sent ) ) );
}

$table = &SESSION->make_element( "table", border=>"0" );
$html->appendChild( &SESSION->html_phrase( "cgi/users/status:index_updates" ) );
$html->appendChild( $table );

$table->appendChild(
	render_row( 
		&SESSION->make_doc_fragment,
		&SESSION->html_phrase( "cgi/users/status:indexupdate" ) ) );
my @ds_ids = &EPrints::DataSet::get_sql_dataset_ids;
foreach my $ds_id ( sort @ds_ids )
{
	my $index = new EPrints::Index( &ARCHIVE->get_dataset( $ds_id ) );
	$table->appendChild(
		render_row( 
			&SESSION->make_text( $ds_id ),
			&SESSION->make_text( $index->get_last_timestamp ) ) );
}

&SESSION->build_page( 
	&SESSION->html_phrase( "cgi/users/status:title" ),
	$html,
	"status" );

&SESSION->send_page(); 

exit;




	
# this cjg should probably by styled.
sub render_row
{
	my( $key, @vals ) = @_;
	
	my( $tr, $td );
	$tr = &SESSION->make_element( "tr" );

	$td = &SESSION->make_element( "td", class=>"status_row_heading" );
	$tr->appendChild( $td );
	$td->appendChild( $key );

	foreach( @vals )
	{
		$td = &SESSION->make_element( "td", align=>"center", class=>"status_cell"  );
		$tr->appendChild( $td );
		$td->appendChild( $_ );
	}
	return $tr;
}
