######################################################################
#
# Enter additional information about a selected item (possibly on 
# behalf of someone else)
#
######################################################################
#
# This file is part of the EPrints RAE module developed by the 
# Institutional Repositories and Research Assessment (IRRA) project,
# funded by JISC within the Digital Repositories programme.
#
# http://irra.eprints.org/
#
# The EPrints RAE module is free software; you can redistributet 
# and/or modify it under the terms of the GNU General Public License 
# as published by the Free Software Foundation; either version 2 of 
# the License, or (at your option) any later version.

# The EPrints RAE module is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty 
# of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
######################################################################

use EPrints::Database;
use EPrints::EPrint;
use EPrints::MetaField;
use EPrints::Session;
use EPrints::User;
use strict;

# Create session
my $session = new EPrints::Session;
exit( 0 ) unless( defined $session );
my $archive = $session->get_archive;

# Check user has appropriate privs
if( !$session->auth_check( "deposit" ) )
{
    $session->terminate;
    exit( 0 );
}
my $user = $session->current_user;


# Check selected role
my $role = $user;
if( defined( $session->param( "role" ) ) )
{
	my $selected_role = EPrints::User->new( $session, $session->param( "role" ) );
	if( $archive->call( "can_user_assume_role", $session, $user, $selected_role ) )
	{
		$role = $selected_role;	
	}
}

# Check eprint
my $eprintid = $session->param("eprintid");
if(!defined $eprintid)
{
	$session->redirect( "select" );
	exit( 0 );
}
my $eprint = EPrints::EPrint->new( $session, $eprintid );
if(!defined $eprint)
{
	$session->redirect( "select" );
	exit( 0 );
}

# Show available roles
my $roles_frag = $session->make_doc_fragment;
my @roles = $archive->call( "roles_for_user", $session, $user );
if( scalar @roles )
{
	my $form = $session->render_form;
	$form->appendChild( $session->render_option_list(name => 'role', pairs => \@roles) );
	$form->appendChild( $session->render_action_buttons(submit => $session->phrase( "rae:change_role" ) ) );
	
	$roles_frag = $session->html_phrase( "rae:roles", 
		change_role => $form,
		current_user => $user->render_description,
	);
}

# Get selection info from DB
my %selection;
my $sql = "SELECT * FROM rae_selections WHERE userid=" . $role->get_id . " AND eprintid=" . $eprint->get_id;
my $sth = $session->get_db->prepare( $sql );
if( $session->get_db->execute( $sth, $sql ) )
{
	my $hashref = $sth->fetchrow_hashref;
	if( defined $hashref )
	{
		%selection = %$hashref;
	}
	else
	{
		# Trying to add info to a non-existent selection
		$session->redirect( "select" );
		exit( 0 );
	}
}

my @metafields;
my @updates;
foreach my $field ( @{ $archive->get_conf( "rae", "fields", "edit" ) } )
{

	# Create metafield	
	my $metafield = EPrints::MetaField->new( 
		archive => $archive,
		confid => "rae",
		%$field,
	);
	push @metafields, $metafield;

	# Check for updated value
	my $action_button = $session->get_action_button;
	if( defined $action_button && $action_button eq "submit" )
	{
		my $value = $metafield->form_value( $session );
		if( defined $value && $value ne $selection{ $metafield->get_name } )
		{
			$selection{ $metafield->get_name } = $value;
			push @updates, sprintf( '%s="%s"', $metafield->get_name, EPrints::Database::prep_value( $value ) );
		}
	}
}

# Update DB
if( scalar @updates ) {
	my $sql = "UPDATE rae_selections SET " . join( ", ", @updates ) . 
		", userid_actual=" . $user->get_id . " WHERE userid=" . 
		$role->get_id . " AND eprintid=" . $eprint->get_id;
	if( !$session->get_db->do( $sql ) ) 
	{
		# Handle error
	}
}

# Render input form
my $form_frag = $session->render_form( "post" );
$form_frag = $session->render_input_form(
	fields => \@metafields,
	values => \%selection,
	show_names => 1,
	show_help => 1,
	buttons => { submit => $session->phrase( "rae/edit:submit_button" ) },
);
$form_frag->appendChild( $session->render_hidden_field( "role", $role->get_id ) ) if $user->get_id != $role->get_id;
$form_frag->appendChild( $session->render_hidden_field( "eprintid", $eprint->get_id ) );

# Build & Send page
my $select_url = $archive->get_conf("perl_url") . "/users/rae/select";
$select_url .= "?role=" . $role->get_id if $user->get_id != $role->get_id;
my $title = $session->html_phrase( "rae/edit:page_title", user => $role->render_description );
my $page = $session->html_phrase( "rae/edit:page",
	form => $form_frag,
	roles => $roles_frag,
	citation => $eprint->render_citation,
	select_link => $session->render_link( $select_url ),
);

$session->build_page( $title, $page );
$session->send_page();
$session->terminate();
exit;
