#!/usr/bin/perl -w -I/opt/ep2stable/perl_lib

######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

=pod

=head1 NAME

B<export> - export all items in a dataset using an output plugin.

=head1 SYNOPSIS

B<export> I<archiveid> [B<options>] I<dataset> [I<plugin>] [I<objectid>] [I<objectid>] ...

=head1 DESCRIPTION

This command outputs a dataset in the selected format. The formats available depend on what Output plugins have been installed.

Some plugins only operate on lists (e.g. RSS), others only operate on single objects only. Some plugins act slightly differently in single-item only mode. For example, outputing a list of eprints as XML gives a number of <eprint> elements in an outer <eprints> element. 

To get the plugin to export a single item use --single. This may not always return a complete file. For example it may be missing the <?xml header, depending on the plugin.

=head1 ARGUMENTS

=over 8

=item I<archiveid> 

The ID of the EPrint archive to use.

=item I<dataset>

The name of the dataset to export, such as "archive", "subject" or "user".

=item I<plugin>

The id of the output plugin to use. This should not include the leading "Output::". Examples: BibTeX, XML.

If this is ommited or an invalid plugin is requested, then 'export' will list all plugins compatible with the dataset and exit.

=item I<objectid>

If this is set then just output the single, specified item. 

=back

=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exit.

=item B<--help>

Print a brief help message and exit.

=item B<--man>

Print the full manual page and then exit.

=item B<--quiet>

Be vewwy vewwy quiet. This option will supress all output unless an error occurs.

=item B<--verbose>

Explain in detail what is going on.
May be repeated for greater effect.

=item B<--version>

Output version information and exit.

=back   


__GENERICPOD__

=cut


use Getopt::Long;
use Pod::Usage;
use strict;

use EPrints::Session;
use EPrints::XML;

my $version = 0;
my $verbose = 0;
my $quiet = 0;
my $purge = 1;
my $help = 0;
my $man = 0;
my $single = 0;

GetOptions( 
	'help|?' => \$help,
	'man' => \$man,
	'version' => \$version,
	'verbose+' => \$verbose,
	'silent' => \$quiet,
	'quiet' => \$quiet,
	'single' => \$single,
) || pod2usage( 2 );
EPrints::Utils::cmd_version( "export_xml" ) if $version;
pod2usage( 1 ) if $help;
pod2usage( -exitstatus => 0, -verbose => 2 ) if $man;
pod2usage( 2 ) if( scalar @ARGV < 2 );

my $noise = 1;
$noise = 0 if( $quiet );
$noise = 1+$verbose if( $verbose );

# Set STDOUT to auto flush (without needing a \n)
$|=1;

my $archiveid = shift @ARGV;
my $datasetid = shift @ARGV;
my $opluginid = shift @ARGV;
my @objectids = @ARGV;

my $session = new EPrints::Session( 1, $archiveid, $noise );
exit( 1 ) unless defined $session;

my $ds = $session->get_archive()->get_dataset( $datasetid ) ;
if( !defined $ds )
{
	print STDERR "Unknown Dataset ID: $datasetid\n";
	$session->terminate;
	exit 1;
}

if( $single && (scalar @objectids) != 1 )
{
	print STDERR "With --single exactly one object ID must be specified.\n";
	$session->terminate;
	exit 1;
}

if( !defined $opluginid )
{
	my @list_plugins = $session->plugin_list( 
					type=>"Output",
					can_accept=>"list/".$ds->confid );
	my @dataobj_plugins = $session->plugin_list( 
					type=>"Output",
					can_accept=>"dataobj/".$ds->confid );
	my %p = ();
	my %l = ();
	my %d = ();
	foreach( @list_plugins ) { $p{$_} = $_; $l{$_} = 1; }
	foreach( @dataobj_plugins ) { $p{$_} = 1; $d{$_} = 1; }
	print "Available output formats:\n";
	foreach my $a_plugin_id ( sort keys %p ) 
	{
		my $a_plugin = $session->plugin( $a_plugin_id );
		print $a_plugin->get_subtype.": ".$a_plugin->get_name();
		if( $l{$a_plugin_id} && !$d{$a_plugin_id} )
		{
			print " (List export only)";
		}
		if( $d{$a_plugin_id} && !$l{$a_plugin_id} )
		{
			print " (Single object export only)";
		}
		print "\n";
	}
	$session->terminate();
	exit;
}

my $pluginid = "Output::".$opluginid;
my $plugin = $session->plugin( $pluginid );

if( $single )
{
	my $objectid = $objectids[0];

	my $req_plugin_type = "dataobj/".$ds->confid;

	unless( $plugin->can_accept( $req_plugin_type ) )
	{
		print STDERR "Plugin $pluginid can't process $req_plugin_type data.\n";
		$session->terminate();
		exit 1;
	}
	my $dataobj = $ds->get_object( $session, $objectid );
	if( !defined $dataobj ) 
	{
		print STDERR "#$objectid not found in ".$ds->id.".\n";
		$session->terminate();
		exit 1;
	}

	print $dataobj->export( $opluginid );
}
else
{
	my $req_plugin_type = "list/".$ds->confid;
	
	unless( $plugin->can_accept( $req_plugin_type ) )
	{
		print STDERR "Plugin $pluginid can't process $req_plugin_type data.\n";
		$session->terminate();
		exit 1;
	}

	@objectids = ("ALL") unless( scalar @objectids );

	my $list = EPrints::List->new(
		dataset => $ds,
		session => $session,
		ids=>\@objectids );

	$plugin->output_list( list=>$list, fh=>*STDOUT );
}

$session->terminate();
exit;
