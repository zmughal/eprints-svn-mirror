######################################################################
#
#  EPrints Object Exporter
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use EPrints::DataSet;

use strict;
my $session = new EPrints::Session;
exit( 0 ) unless( defined $session );
# $session->get_db->set_debug( 1 );

my $path_info = $session->get_request->path_info;

unless( $path_info =~ m!^/([0-9]+)(/([^/]*)?)?! ) {
	error( $session, $session->make_text("Error: invalid export.") );
}

my $id = $1;
my $afterid = $2;
my $format = $3;
my $export_url = $session->get_archive->get_conf( "perl_url" )."/export";

if( !defined $id ) {
	error( $session, $session->make_text("No valid ID specified") );
}
if( !defined $afterid ) {
	$session->redirect( $export_url."/".$id."/" );
	$session->terminate;
	exit;
}

my $eprint = EPrints::EPrint->new( 
			$session, 
			$id, 
			$session->get_archive->get_dataset( "archive" ) );
if( !defined $eprint ) {
	error( $session, $session->make_text("EPrint with $id is not in repository.") );
}

if( !defined $format || $format eq "" ) {
	my $title = $session->make_text("Export EPrint $id");
	my $page= $session->make_doc_fragment;
	$page->appendChild( render_export_links( $eprint, $export_url ) );
	$session->build_page( $title, $page, "export" );
	$session->send_page;
	$session->terminate;
	exit;
}

my @plugins = $session->plugin_list( can_accept=>"dataobj/eprint", is_visible=>"all" );
my $ok = 0;
foreach( @plugins ) { if( $_ eq "output/$format" ) { $ok = 1; last; } }
unless( $ok ) {
	error( $session, $session->make_text("Format '$format' not available." ) );
}

my $plugin = $session->plugin( "output/$format" );
$session->send_http_header( "content_type"=>$plugin->call( "mime_type" ) );
print $eprint->export( $format );	
	
$session->terminate;
exit;

sub error
{
	my( $session, $msg ) = @_;

	$session->build_page( 
		$session->make_text( "Error in EPrint Export" ),
		$msg,
		"export_error" );
	$session->send_page;
	$session->terminate;
	exit;
}

# planned to move into EPrints::EPrint
sub render_export_links
{
	my( $self ) = @_;

	my $id = $self->get_value( "eprintid" );
	my $ul = $self->{session}->make_element( "ul" );
	my @plugins = $self->{session}->plugin_list( 
					can_accept=>"dataobj/eprint", 
					is_visible=>"all" );
	foreach my $plugin_id ( @plugins ) {
		my $li = $self->{session}->make_element( "li" );
		my $plugin = $self->{session}->plugin( $plugin_id );
		my $url = $plugin->call( "dataobj_export_url", $self );
		my $a = $self->{session}->render_link( $url );
		$a->appendChild( $plugin->call( "render_name" ) );
		$li->appendChild( $a );
		$ul->appendChild( $li );
	}
	return $ul;
}



