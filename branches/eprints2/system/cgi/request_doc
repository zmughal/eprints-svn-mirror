#!/usr/bin/perl -w -I/opt/eprints2/perl_lib

use EPrints;
use strict;

my $session = new EPrints::Session();
exit( 1 ) unless( defined $session );

# Check parameters
my $eprintid = $session->param( "eprintid" );
error_page( $session, $session->html_phrase( "request_doc/error" ) ) if !defined( $eprintid );
my $eprint = EPrints::DataObj::EPrint->new( $session, $eprintid );
error_page( $session, $session->html_phrase( "request_doc/error" ) ) if !defined( $eprint );

# Check request
my $status = $eprint->get_value( "full_text_status" );
error_page( $session, $session->html_phrase( "request_doc/error:bad_request" ), $eprint->get_url ) 
	if $status eq "public";

# Check contact email for this eprint
my $contact_email = undef;
if( $session->get_repository->can_call( "email_for_doc_request" ) ) 
{
	$contact_email = $session->get_repository->call( "email_for_doc_request", $session, $eprint );
}
error_page( $session, $session->html_phrase( "request_doc/error:no_contact" ), $eprint->get_url ) if !defined( $contact_email );

my $title = $session->html_phrase( "request_doc/page:title_$status" );
my $page = $session->make_doc_fragment();

my $email = $session->param( "email" );
if( defined( $email ) && $email ne "" )
{
	# Send request email
	my $subject = $session->phrase( "request_doc/request:subject", eprint => $eprint->get_value( "title" ) );
	my $mail = $session->make_element( "mail" );
	$mail->appendChild($session->html_phrase( "request_doc/request:body", 
		eprint 		=> $session->make_text( EPrints::Utils::tree_to_utf8( $eprint->render_citation ) ),
		url 		=> $session->make_text( $eprint->get_url ),
		requester	=> $session->make_text( $email ) ) );

	my $result;
	my $user = EPrints::DataObj::User::user_with_email( $session, $contact_email );
	if( defined( $user ) && $status eq "restricted" )
	{
		# Contact is registered user and EPrints holds restricted documents
		# Send email to contact with accept/reject links
		# TODO: does encode_str do the right thing?
		my $url =  $session->get_repository->get_conf( "perl_url" ) . 
			"/users/respond_doc?eprintid=" . $eprint->get_id . 
			"&email=" . EPrints::Utils::encode_str( $session->param( "email" ) );
		$mail->appendChild( $session->html_phrase( "request_doc/request:links",
			accept => $session->make_text( "$url&action=accept" ),
			reject => $session->make_text( "$url&action=reject" ) ) );

		$result = EPrints::Utils::send_mail(
			$session->get_repository,
			$session->get_langid,
			EPrints::Utils::make_name_string( $user->get_value( "name" ), 1 ),
			$contact_email,
			$subject,
			$mail,
			$session->html_phrase( "mail_sig" )
		);
	} 
	else
	{
		# Contact is non-registered user or EPrints holds no documents
		# Send email to contact with 'replyto'
		$result = EPrints::Utils::send_mail(
			$session->get_repository,
			$session->get_langid,
			"",
			$contact_email,
			$subject,
			$mail,
			$session->html_phrase( "mail_sig" ),
			$email, # replyto
		);

	}

	if( !$result )
	{
		error_page( $session, $session->html_phrase( "request_doc/error:email" ), '?eprintid=' . $eprint->get_id );
	}
	else
	{
		# Send acknowledgement to requester
		# TODO: worry about the result?
		my $mail = $session->make_element( "mail" );
		$mail->appendChild( $session->html_phrase( "request_doc/ack:body", 
			eprint	=> $session->make_text( EPrints::Utils::tree_to_utf8( $eprint->render_citation ) ),
			url	=> $session->make_text( $eprint->get_url ) ) );
		EPrints::Utils::send_mail(
			$session->get_repository,
			$session->get_langid,
			"",
			$email,
			$session->phrase( "request_doc/ack:subject", eprint=>$eprint->get_value( "title" ) ), 
			$mail,
			$session->html_phrase( "mail_sig" )
		);

		# Render confirmation page
		$page->appendChild( $session->html_phrase( "request_doc/ack",
			link => $session->render_link( $eprint->get_url ),
		) );
		$session->build_page( $title, $page );
		$session->send_page();
		$session->terminate();
		exit( 1 );
	}
}

# Render requested eprint
my $p = $session->make_element( "p" );
$page->appendChild( $p );
$p->appendChild( $eprint->render_citation );

# Render request form
my $form =  $session->render_form( "post" );
$page->appendChild( $form );
$form->appendChild( $session->html_phrase( "request_doc/page:fieldname_email" ) );
$form->appendChild( $session->make_element( "input", type => "text", name => "email" ) );
$form->appendChild( $session->make_element( "br" ) );
$form->appendChild( $session->render_hidden_field( "eprintid", $eprintid ) );
$form->appendChild( $session->render_action_buttons( submit => $session->phrase( "request_doc/page:button_request" ) ) );

$session->build_page( $title, $page );
$session->send_page();

$session->terminate();


sub error_page {
	my ( $session, $msg, $back_to ) = @_;
	$session->render_error( $msg, $back_to );
	$session->terminate();
	exit( 1 );
}
