#!/usr/bin/perl -w -I/opt/eprints2/perl_lib

use EPrints;
use strict;

my $session = new EPrints::Session();
exit( 1 ) unless( defined $session );

my ( $eprint, $doc );

# Get requested document
my $docid = $session->param( "docid" );
$doc = EPrints::DataObj::Document->new( $session, $docid );
if( !defined $doc )
{
	$session->render_error( $session->html_phrase( 
		"cgi/users/edit_eprint:cant_find_it", 
		id => $session->make_text( $docid ) ) );
	$session->terminate();
	exit( 1 );
}
$eprint = $doc->get_eprint;

my $email = $session->param( "email" );
if( !defined( $email ) || $email eq "" )
{
	$session->render_error( $session->html_phrase( "general:bad_param" ) );
	$session->terminate();
	exit( 1 );
}

# Check current user can respond
my $contact_email = undef;
if( $session->get_repository->can_call( "email_for_doc_request" ) )
{
	$contact_email = $session->get_repository->call( "email_for_doc_request", $session, $eprint );
}
if( !defined( $contact_email ) || $session->current_user->get_value( "email" ) ne $contact_email )
{
	$session->render_error( $session->html_phrase( 
		"request/error:not_auth" ) );
	$session->terminate();
	exit( 1 );
}

my $title = $session->html_phrase( "request/respond_page:title" );
my $page = $session->make_doc_fragment();

my $action = $session->param("action");
$action = "reject" if $action ne "accept";

# Check whether requested document been made OA in the meantime
my $security = $doc->get_value( "security" );
$action = "oa" if !defined $security || $security eq "";

my $button = $session->get_action_button;
unless( defined $button && $button eq "submit" )
{

	$page->appendChild( $session->html_phrase( "request/respond_page:$action",
		eprint => $eprint->render_citation_link,
		document => $doc->render_citation_link
	) );

	# Render response form
	my $form =  $session->render_form( "post" );
	$page->appendChild( $form );

	if( $action eq "reject" )
	{
		my $textarea = $session->make_element( "textarea", 
			name => "reason",
			"accept-charset" => "utf-8",
			rows => 5,
			cols => 60,
			wrap => "virtual",
		);
		$textarea->appendChild( $session->make_text( "" ) );
		$form->appendChild( $textarea );
	}

	if( $action eq "accept" )
	{
		my $p = $session->make_element( "p" );
		$form->appendChild( $p );
		my $cb = $session->make_element( "input", type => "checkbox", name => "oa" );
		$cb->appendChild( $session->html_phrase(
			"request/respond_page:fieldname_oa" ) );
		$p->appendChild( $cb );
	}

	$form->appendChild( $session->make_element( "br" ) );
	$form->appendChild( $session->render_hidden_field( "docid", $doc->get_id ) );
	$form->appendChild( $session->render_hidden_field( "email", $email ) );
	$form->appendChild( $session->render_hidden_field( "action", $action ) );
	$form->appendChild( $session->render_action_buttons( submit => $session->phrase( "request/respond_page:action_respond" ) ) );

	$session->build_page( $title, $page );
	$session->send_page();

	$session->terminate();
	exit( 0 );
}

my $subject = $session->phrase( 
	"request/response_email:subject", 
	eprint => $eprint->get_value( "title" ) );

my $mail = $session->make_element( "mail" );
my $reason = $session->param( "reason" );
$mail->appendChild( $session->html_phrase(
	"request/response_email:body_$action",
	eprint => $eprint->render_citation_link,
	document => $doc->render_citation_link,
	reason => EPrints::Utils::is_set( $reason ) ? $session->make_text( $reason )
		: $session->html_phrase( "cgi/users/review:no_reason" ) ) );

my $result;
if( $action eq "accept")
{
	# Send acceptance notice with requested document attached
	my @paths;
	my %files = $doc->files;
	for( keys %files )
	{
		push @paths, $doc->local_path . "/" . $_;
	}

	# Make document OA if flag set
	if( defined $session->param( "oa" ) && $session->param( "oa" ) eq "on" )
	{
		$doc->set_value( "security", "" );
		$doc->commit;
		$eprint->commit;
		$eprint->generate_static;
	}

	$result = EPrints::Utils::send_mail(
		session => $session,
		langid => $session->get_langid,
		to_email => $email,
		subject => $subject,
		message => $mail,
		sig => $session->html_phrase( "mail_sig" ),
		attach => \@paths,
	);
}
else
{
	# Send rejection notice
	$result = EPrints::Utils::send_mail(
		session => $session,
		langid => $session->get_langid,
		to_email => $email,
		subject => $subject,
		message => $mail,
		sig => $session->html_phrase( "mail_sig" ),
	);
}

if( !$result )
{
	$session->render_error( $session->html_phrase( "general:email_failed" ) );
	$session->terminate();
	exit( 1 );
}
else
{
	# Render confirmation page
	$page->appendChild( $session->html_phrase( "request/response:ack_page" ) );
	$session->build_page( $title, $page );
	$session->send_page();
	$session->terminate();
	exit( 1 );
}

