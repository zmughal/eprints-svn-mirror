######################################################################
#
#  EPrint Editor
#
#   Allows staff to remove EPrints or transfer them back to the
#   submission buffer.
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::EPrint;
use EPrints::Session;

use strict;

#cjg check item is editable by this user.

#cjg remove does not work

my $session = EPrints::Session->new();

# Check we have privs
if( !$session->auth_check( "editor" ) )
{
	$session->terminate();
	exit( 0 );
}

my( $title, $page ) = &process( $session );

$session->terminate();



sub process
{
	my( $session ) = @_;

	my $datasetid = $session->param( "dataset" );

	# When in doubt, use the main archive.

	my $eprintid = $session->param( "eprintid" );
	
	my $dataset;
	if( defined $datasetid )
	{
		$dataset = $session->get_archive()->get_dataset( $datasetid );
	}

	my $eprint = new EPrints::EPrint( $session, $eprintid, $dataset );

	if( !defined $eprint )
	{
		$session->render_error( $session->html_phrase(
			"cgi/users/edit_eprint:cant_find_it",
			id=>$session->make_text( $eprintid ) ) );
		return;
	}

	if( !defined $dataset )
	{
		$dataset = $eprint->get_dataset;
	}

	my $user_ds = $session->get_archive()->get_dataset( "user" );

	my $can_edit = 0;
	my $ef_field = $user_ds->get_field( 'editperms' );
	my $searches = $session->current_user->get_value( 'editperms' );
	if( scalar @{$searches} == 0 )
	{
		$can_edit = 1;
	}

	foreach my $s ( @{$searches} )
	{
		next if( $can_edit ); # skip the rest if one matches

		my $search = $ef_field->make_searchexp( $session, $s );
		if( $search->get_conditions->item_matches( $eprint ) )
		{
			$can_edit = 1;
		}
		$search->dispose;
	}

	unless( $can_edit )
	{
		$session->render_error( $session->html_phrase(
			"cgi/users/edit_eprint:cant_edit",
			id=>$session->make_text( $eprintid ) ) );
		return;
	}

	my $action = $session->get_action_button();

	if( !defined $action )
	{
		&view_page( $session, $eprint );
		return;
	}

	if( $action eq "_toinbox" )
	{
		# Bounce button pressed - get reason
		&bounce_form( $session, 0, $eprint );
		return;
	}

	if( $action eq "_remove" )
	{
		# Remove button pressed - get reason
		&bounce_form( $session, 1, $eprint );
		return;
	}

	if( $action eq "_send" )
	{
		# Actually do the bounce
		&bounce( $session, $eprint );
		return;
	}

	if( $action eq "_toarchive" )
	{	
		# Accept button pressed

		my $dsid = $eprint->get_dataset()->id();

		unless( $eprint->move_to_archive() )
		{
			$session->render_error( $session->html_phrase(
				"cgi/users/edit_eprint:cant_move",
				id=>$session->make_text( $eprintid ) ) );
			return;
		}

		# Successfully archived, redirect
		if( $dsid eq "buffer" )
		{
			$session->redirect( "buffer" );
		}
		else
		{
			$session->redirect( $session->get_archive()->get_conf( "userhome" ) );
		}
		return;
	}

	if( $action eq "_tobuffer" )
	{
		
		unless( $eprint->move_to_buffer() )
		{
			$session->render_error( $session->html_phrase(
				"cgi/users/edit_eprint:cant_move",
				id=>$session->make_text( $eprintid ) ) );
			return;
		}

		my $page = $session->make_doc_fragment();
		$page->appendChild( $session->html_phrase( 
			"cgi/users/edit_eprint:moved",
			link=>$session->render_link( "edit_eprint?dataset=buffer&eprintid=".$eprintid ) ) );
		$page->appendChild( $session->html_phrase( 
			"general:userhome_link" ) );
		$session->build_page( $session->html_phrase( "cgi/users/edit_eprint:move_title" ), $page, "move_eprint" );
		$session->send_page();
		return;
	}


	if( $action eq "_todeletion" )
	{
		unless( $eprint->move_to_deletion() )
		{
			$session->render_error( $session->html_phrase(
				"cgi/users/edit_eprint:cant_remove",
				id=>$session->make_text( $eprintid ) ) );
			return;
		}
		my $page = $session->make_doc_fragment();
		$page->appendChild( $session->html_phrase( 
			"cgi/users/edit_eprint:removed" ) );
		$page->appendChild( $session->html_phrase( 
			"general:userhome_link" ) );
		$session->build_page( $session->html_phrase( "cgi/users/edit_eprint:remove_title" ), $page, "move_removed" );
		$session->send_page();
		return;
	}


	if( $action eq "_clone" || $action eq "_copy" )
	{
		my $target_ds_id = 'buffer';
		if( $session->get_archive->get_conf( "skip_buffer" ) )
		{
			$target_ds_id = 'archive';
		}
		my $target_ds = $session->get_archive()->get_dataset( $target_ds_id );
		my $copy;
		if( $action eq "_clone" )
		{
			# with files, with link
			$copy = $eprint->clone( $target_ds, 1 );
		}
		else
		{
			# nofiles, no link
			$copy = $eprint->clone( $target_ds, 0, 1 );
		}
	
		if( defined $copy )
		{
			# Copied OK, redirect to buffer
			$session->redirect( 'edit_eprint?eprintid='.$copy->get_id.'&_action_edit=1' );
			return;
		}
		
		$session->render_error( $session->html_phrase(
			"cgi/users/edit_eprint:cant_clone",
			id=>$session->make_text( $eprintid ) ) );
		return;
	}




	#########################################################
	
	# OK, so we are (presumably) editing it then...

	my $stage = $session->param( "stage" );
	# If we are skipping the files stage then we have to work out what the
	# actual last stage is...
	my $laststage = "files";
	my @stages = ( "type","linking","meta" );
	while( $session->get_archive()->get_conf( "submission_stage_skip", $laststage ) )
	{
		$laststage = pop @stages;
		last if( $laststage eq "type" );
	}

	my $ls = $session->get_archive()->get_conf( "submission_stage_last_for_staff_edit" );
	$laststage = $ls if( defined $ls );

	if( defined $stage && $laststage eq $stage && defined $action && ($action eq "finished" || $action eq "next") )
	{
		my $redir = 1;
		if( $stage eq "meta" )
		{
			my @pages = $dataset->get_type_pages( $eprint->get_value( "type" ) );
			my $pageid = $session->param( "pageid" );
			$redir = 0;
			$redir = 1 if( $pageid eq pop @pages );
		}
		if( $redir )
		{
			# Intercept the verify page, that's what we were doing!
			$session->redirect( "edit_eprint?dataset=$datasetid&eprintid=$eprintid" );
			return;
		}
	}

	# Give other cases to the edit form
	my $subform = new EPrints::SubmissionForm(
		$session,
		"edit_eprint?dataset=$datasetid&eprintid=$eprintid",
		1,
		$dataset,
		"edit_eprint" );

	$subform->process();
	
	if( $dataset->id eq "archive" || $dataset->id eq "deletion" )
	{
		# If the eprint is in the main archive or deletion area
		# then we need to update its webpage. This will make editing
		# even slower, but editing the main db SHOULD be a rare thing
		# anyway.

		# get it from the DB again - it's probably changed.
		my $eprint = new EPrints::EPrint( 
			$session, 
			$eprintid,
			$dataset );

		# update the static pages.
		$eprint->generate_static;
	}
	return;

}

# Show metadata & options:

sub view_page
{
	my( $session, $eprint ) = @_;

	my $page = $session->make_doc_fragment();
	
	$page->appendChild( $session->html_phrase( 
		"cgi/users/edit_eprint:status",
		dataset => $session->html_phrase( "dataset_fieldopt_dataset_".$eprint->get_dataset()->id() ) ) );
	
	$page->appendChild( $eprint->render_full() );
			
	# Possible actions
	# inbox buffer archive deletion
	#   *     *       *       *      edit
	#   *     *                      remove - and send message to depositing user
	#         *                      move to inbox - and send message to user
	#   *             *              move to buffer  - "
	#         *               *      move to archive  - "
	#                 *              move to deletion  - "
	#                 *       *      clone(new version) to buffer
	#   *     *       *       *      copy(as template) to buffer

	# actions for this form begin with _ (except edit and
	# those passed to submission form)

	my $sb = $session->get_archive()->get_conf( "skip_buffer" );	
	my $buttons = {};
	my $r1 = [];
	my $r2 = [];
	if( $eprint->get_dataset()->id() eq "inbox" )
	{
		if( defined $sb && $sb == 1 )
		{
			$r1 = [ "_toarchive" ];
		}
		else
		{
			$r1 = [ "_tobuffer" ];
		}
		$r2 = [ "edit", "_remove", "_copy" ];
	}
	if( $eprint->get_dataset()->id() eq "buffer" )
	{
		$r1 = [];
		# only offer to return this to the inbox
		# if it's owned by a valid user.
		if( defined $eprint->get_user() )
		{
			push @{$r1}, "_toinbox";
		}
		push @{$r1}, "_toarchive";
		$r2 = [ "edit", "_remove", "_copy" ];
	}
	if( $eprint->get_dataset()->id() eq "archive" )
	{
		if( defined $sb && $sb == 1 )
		{
			$r1 = [ "_toinbox", "_todeletion" ];
		}
		else
		{
			$r1 = [ "_tobuffer", "_todeletion" ];
		}
		$r2 = [ "edit", "_clone", "_copy" ];
	}
	if( $eprint->get_dataset()->id() eq "deletion" )
	{
		$r1 = [ "_toarchive" ];
		$r2 = [ "edit", "_clone", "_copy" ];
	}


	my $form = $session->render_form( "post", "edit_eprint" );
	foreach( @{$r1}, @{$r2} )
	{
		$buttons->{$_} = $session->phrase( "cgi/users/edit_eprint:action_".$_ );
	}
	#print STDERR "**". $eprint->get_dataset()->id()."**\n";
	#print STDERR "**". join( '/', (@{$r1}, @{$r2}))."**\n";
	
	$form->appendChild( $session->render_ruler() );
	$buttons->{_order} = $r1;
	$form->appendChild( $session->render_action_buttons( %{$buttons} ) );

	$form->appendChild( $session->render_ruler() );
	$buttons->{_order} = $r2;
	$form->appendChild( $session->render_action_buttons( %{$buttons} ) );

	$form->appendChild( $session->render_hidden_field( "eprintid", $eprint->get_id() ) );
	$form->appendChild( $session->render_hidden_field( "dataset", $eprint->get_dataset()->id() ) );

	$page->appendChild( $form );
	$page->appendChild( $session->html_phrase( "general:userhome_link" ) );
		
	$session->build_page( $session->html_phrase( "cgi/users/edit_eprint:form_title" ), $page, "move_form" );
	$session->send_page();
}


sub bounce_form
{
	my( $session, $delete, $eprint ) = @_;

	# Get the user's details
	my $user = $eprint->get_user();
	# We can't bounce it if there's no user associated - but
	# we can still delete it.
	if( !defined $user && !$delete)
	{
		$session->render_error( 
			$session->html_phrase( "cgi/users/edit_eprint:no_user" ),
			"buffer" );
		return;
	}

	my $page = $session->make_doc_fragment();

	my $form = $session->render_form( "post", "edit_eprint" );
	if( defined $user )
	{
		$page->appendChild( $session->html_phrase( "cgi/users/edit_eprint:bounce_form_intro", langpref=>$user->render_value( "lang" ) ) );
		my $div = $session->make_element( "div", class => "formfieldinput" );
		my $textarea = $session->make_element(
			"textarea",
			name => "reason",
			rows => 20,
			cols => 60,
			wrap => "virtual" );
		# remove any markup:
		my $title = $session->make_text( EPrints::Utils::tree_to_utf8( $eprint->render_description() ) );
	
		$textarea->appendChild( $session->html_phrase(
			( $delete ? "mail_delete_reason" : "mail_bounce_reason" ),
			title => $title ) );
		$div->appendChild( $textarea );
		$form->appendChild( $div );

		$form->appendChild( $session->render_action_buttons(
			"_send" => $session->phrase( "cgi/users/edit_eprint:action_send" ) ) );
	}
	else
	{
		my $p = $session->make_element( "p" );
		$p->appendChild( $eprint->render_description() );
		$form->appendChild( $p );

		$form->appendChild( $session->render_action_buttons(
			"_send" => $session->phrase( "cgi/users/edit_eprint:action_reallydelete" ) ) );
	}
	

	$form->appendChild( $session->render_hidden_field( "eprintid", $eprint->get_value( "eprintid" ) ) );
	$form->appendChild( $session->render_hidden_field( "dataset", $eprint->get_dataset()->id() ) );
	$form->appendChild( $session->render_hidden_field( "delete", $delete ) );

	$page->appendChild( $form );

	$session->build_page(
		$session->html_phrase( "cgi/users/edit_eprint:title_".( $delete ? "delete" : "bounce" )."_form" ),
		$page,
		"editeprint_form" );
	$session->send_page();
}


sub bounce
{
	my( $session, $eprint ) = @_;

	my $delete = $session->param( "delete" );

	# Get the user's details
	my $user = $eprint->get_user();
	# We can't bounce it if there's no user associated - but
	# we can still delete it.
	if( !defined $user && !$delete)
	{
		# Can't find the user
		$session->render_error( 
			$session->html_phrase( "cgi/users/edit_eprint:no_user" ),
			"buffer" );
		return;
	}
	
	my $success = 0;
	
	if( $delete )
	{
		# Delete the submission
		$success = $eprint->remove();
	}
	else
	{
		# Transfer the EPrint back to the user's inbox
		$success = $eprint->move_to_inbox();
	}
	
	unless( $success )
	{
		# Couldn't be bounced at all
		$session->render_error( 
			$session->html_phrase( "cgi/users/edit_eprint:bord_fail" ),
			"buffer" );
		return;
	}

	if( !defined $user )
	{
		# Can't send mail to a user what does not
		# exist.
		$session->redirect( "buffer" );
		return;
	}

	my $mail = $session->make_element( "mail" );
	$mail->appendChild( $session->make_text( $session->param( "reason" ) ) );

	# Successfully transferred, mail the user with the reason
	if( $user->mail(
		"cgi/users/edit_eprint:subject_bounce",
		$mail,
		$session->current_user() ) )
	{	
		# Successfully bounced, redirect
		$session->redirect( "buffer" );
		return;
	}

	# Couldn't mail
	$session->render_error( 
		$session->html_phrase( "cgi/users/edit_eprint:mail_fail",
			username=>$user->render_value( "username" ),
			email=>$user->render_value( "email" ) ),
		"buffer" );
}
