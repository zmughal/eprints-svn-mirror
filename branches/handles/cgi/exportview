######################################################################
#
#  EPrints Object Exporter
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;

use strict;
use Data::Dumper;

my $handle = new EPrints::Handle;
exit( 0 ) unless( defined $handle );
# $handle->get_database->set_debug( 1 );

if( defined $handle->param( "view" ) )
{
	# hmm, got GET parameters, not a path...
	my $viewid = $handle->param( "view" );
	my $values = $handle->param( "values" );
	my $format = $handle->param( "format" );

	my $plugin = get_plugin( $handle, $format );
	if( !defined $plugin )
	{
		error( $handle, $handle->html_phrase( "cgi/export:not_available",
					format => $handle->make_text( $format ) ) );
		$handle->terminate;
		exit;
	}

	my $export_url = $handle->get_repository->get_conf( "perl_url" )."/exportview";
	my $fn = $values;
	$fn =~ s/\//_/g;
	$export_url .= "/$viewid/$values/$format/$fn".$plugin->param("suffix");
	$handle->redirect( $export_url );
	exit;
}


my $path_info = $handle->get_request->path_info;
#http://devel.eprints.org/cgi/exportview/person/Nittrouer=3AX=2E=3A=3A/HTML/Nittrouer=3AX=2E=3A=3A.html
unless( $path_info =~ m!^/([^/]+)/(.*)/([^/]+)/[^/]+$! )
{
	error( $handle, $handle->html_phrase( "cgi/export:no_id" ) );
	$handle->terminate;
	exit;
}
my $viewid = $1;
my @view_path = split( '/', $2 );
my $format = $3;

my $ds = $handle->get_repository->get_dataset( "archive" );

my $view;
foreach my $a_view ( @{$handle->get_repository->get_conf( "browse_views" )} )
{
	$view = $a_view if( $a_view->{id} eq $viewid );
}
if( !defined $view )
{
	EPrints::abort( "view with ID '$viewid' is not available." );
}

my $filters = EPrints::Update::Views::get_filters( $handle, $view, \@view_path );
if( !defined $filters )
{
	$handle->not_found; #404
	$handle->terminate;
	exit;
}

my $search = new EPrints::Search(
				custom_order=>$view->{order},
				satisfy_all=>1,
				handle =>$handle,
				dataset=>$ds );
$search->add_field( $ds->get_field('metadata_visibility'), 'show', 'EQ' );
my $n=0;
foreach my $filter ( @{$filters} )
{
	$search->add_field( $filter->{fields}, $filter->{value}, "EX", undef, "filter".($n++), 1 );
}

my $plugin = get_plugin( $handle, $format );
if( !defined $plugin )
{
	error( $handle, $handle->html_phrase( "cgi/export:not_available",
				format => $handle->make_text( $format ) ) );
	$handle->terminate;
	exit;
}

$handle->send_http_header( "content_type"=>$plugin->param("mimetype") );

my $list = $search->perform_search;
print $list->export( $format );	
	
$list->dispose;

$handle->terminate;
exit;

sub error
{
	my( $handle, $msg ) = @_;

	$handle->prepare_page( 
		title=>$handle->html_phrase( "cgi/export:error_title" ),
		page=>$msg,
		page_id=>"export_error" );
	$handle->send_page;
}

sub get_plugin
{
	my( $handle, $format ) = @_;

	my @plugins = $handle->plugin_list( 
				type=>"Export", 
				can_accept=>"list/eprint", 
				is_visible=>"all" );
	my $ok = 0;
	foreach( @plugins ) 
	{ 
		if( $_ eq "Export::$format" ) 
		{
			return $handle->plugin( "Export::$format" );
		}
	}
	return;
}

