######################################################################
#
#  Show EPrints modified or added in the past 7 days
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;

use strict;
my $handle = new EPrints::Handle;
exit( 0 ) unless( defined $handle );

my $ds = $handle->get_repository->get_dataset( "archive" );
my $page=$handle->make_doc_fragment();

$page->appendChild( $handle->html_phrase( "cgi/latest:intro" ) );

my $citation = $handle->get_repository->get_conf( "latest_citation" );

my $seensome = 0;
for( my $d=0; $d<7; ++$d )
{
	my $t = time-60*60*24*$d;
	my $search = new EPrints::Search(
		filters => [
			{ 
				meta_fields=>[ 'metadata_visibility' ], 
				value=>'show', 
				match=>'EX', 
				describe=>0,
			},
		],	
		custom_order => "-datestamp",
		handle => $handle,
		dataset => $ds,
	);

	my $date = EPrints::Time::get_iso_date( $t );
	$search->add_field( $ds->get_field( "datestamp" ), $date );
		
	my $list = $search->perform_search();

	if( $list->count() )
	{
		$seensome = 1;

		my $day;
		if( $d == 0 )
		{
			$day = $handle->html_phrase( "cgi/latest:today" );
		}
		elsif( $d == 1 )
		{
			$day = $handle->html_phrase( "cgi/latest:yesterday" );
		}
		else
		{
			my $dow = (localtime($t))[6];
			$day = $handle->html_phrase( "cgi/latest:day_".$dow );
		}

		my $h2= $handle->make_element( "h2" );
		$h2->appendChild( $day );
		$page->appendChild( $h2 );

		my $type = $handle->get_citation_type( $ds, $citation );
		my $container;
		if( $type eq "table_row" )
		{
			$container = $handle->make_element( 
					"table", 
					class=>"ep_latest_list" );
		}
		else
		{
			$container = $handle->make_element( 
					"div", 
					class=>"ep_latest_list" );
		}
		$page->appendChild( $container );
		my $n = 1;
		$search->map( sub{ 
			my( $handle, $dataset, $item, $info ) = @_;
			my $row = $item->render_citation_link( 
				$citation,
				n => [$n++, "INTEGER"] );
			if( $type eq "table_row" )
			{
				$container->appendChild( $row );
			}
			else
			{
				my $div = $handle->make_element( 
					"div", 
					class=>"ep_latest_result" );
				$div->appendChild( $row );
				$container->appendChild( $div );
			}
		} );
	
		$page->appendChild( $handle->render_ruler() );
	}
	$search->dispose();
}
unless( $seensome )
{
	$page->appendChild( $handle->html_phrase( "cgi/latest:none" ) );
}

$page->appendChild( $handle->html_phrase( "general:frontpage_link" ) );
	
my $title = $handle->html_phrase( "cgi/latest:title" );
$handle->prepare_page( title=>$title, page=>$page, page_id=>"latest" );
$handle->send_page();

$handle->terminate;
