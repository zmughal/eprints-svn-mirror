# warns of duplicates based on a simple title string match

use EPrints;

use strict;
use warnings;

my $handle = EPrints::Handle->new();

# security?

# How many chars needed before we try and suggest duplicates?
my $need = 5;

my $q = $handle->param( "q" ) || "";

exit unless length( $q ) >= $need; 

my $eprintid = $handle->param( "eprintid" );
if( !defined($eprintid) )
{
	$handle->get_repository->log( "title_duplicates lookup script called without eprintid argument: ".join(',',$handle->param));
	exit(0);
}
elsif( $eprintid =~ /^([0-9]+)$/ )
{
	$eprintid = $1;
}
else
{
	EPrints::abort "Requires numeric eprintid argument";
}

my $database = $handle->get_database;
my $dataset = $handle->get_repository->get_dataset( "eprint" );

my $Q_table = $database->quote_identifier($dataset->get_sql_table_name);
my $Q_eprintid = $database->quote_identifier( "eprintid" );
my $Q_eprint_status = $database->quote_identifier( "eprint_status" );
my $Q_title = $database->quote_identifier( "title" );

my $sql = "SELECT $Q_eprintid" .
	" FROM $Q_table" .
	" WHERE " .
	" $Q_eprint_status=" .  $database->quote_value( "archive" ) .
	" AND $Q_eprintid!=$eprintid" .
	" AND $Q_title IS NOT NULL" .
	" AND $Q_title LIKE " .
	$database->quote_value( EPrints::Database::prep_like_value( $q ) . '%' );

my $doc = $handle->make_doc_fragment();

my $sth = $handle->get_database->prepare_select( $sql, 'limit' => 10 );
$handle->get_database->execute( $sth , $sql );

my $base_url = $handle->get_repository->get_conf( "base_url" );

my $warning =<<END;
The following records matching this title already exist in the archive.
Please check that you are not entering a duplicate record.
END

my $row = $sth->fetch;

exit unless defined $row;

$doc->appendChild( my $div = $handle->render_message( "warning", $handle->make_text( $warning) ) );

$div->appendChild( my $ul = $handle->make_element( 'ul' ));

my $count = 0;
my $first = 1;

do
{
	my( $eprintid, $title ) = @$row;
	$ul->appendChild( my $li = $handle->make_element( 'li' ) );
	if( $first )
	{
		$li->setAttribute( 'class', 'ep_first' );
		$first = 0;
	}
	my $eprint = EPrints::DataObj::EPrint->new( $handle, $eprintid );
	if( ++$count > 5 )
	{
		$li->appendChild( $eprint->render_citation_link( "brief", target => "_blank" ) );
	}
	else
	{
		$li->appendChild( $eprint->render_citation_link( "default", target=>"_blank" ) );
	}
} while( $count < 20 && defined($row = $sth->fetch) );

$sth->finish;

$handle->send_http_header( content_type => "text/xml; charset=UTF-8" );

print <<END,
<?xml version="1.0" encoding="UTF-8" ?>

END
	EPrints::XML::to_string( $doc, "utf-8", 1 );

EPrints::XML::dispose( $doc );

$handle->terminate;
