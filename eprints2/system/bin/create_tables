#!/usr/bin/perl -w -I/opt/eprints3/perl_lib

######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

=pod

=head1 NAME

B<create_tables> - create SQL tables for an EPrints repository

=head1 SYNOPSIS

B<create_tables> I<repository_id> [B<options>]

=head1 DESCRIPTION

Create the SQL tables which the EPrints software will use. 

You should run B<import_subjects> after this, actually it will remind you unless you make it B<--quiet>.

=head1 ARGUMENTS

=over 8

=item I<repository_id> 

The ID of the EPrint repository to effect.

=back

=head1 OPTIONS

=over 8

=item B<--force>

Go ahead and try and create the tables, even if the script encounters things it thinks are wrong like too many indexed fields or too the database not being empty.

=item B<--help>

Print a brief help message and exit.

=item B<--man>

Print the full manual page and then exit.

=item B<--quiet>

Be vewwy vewwy quiet. This option will supress all output unless an error occurs.

=item B<--verbose>

Explain in detail what is going on. 
May be repeated for greater effect.

=item B<--version>

Output version information and exit.

=back   

__GENERICPOD__

=cut


use EPrints;

use strict;
use Getopt::Long;
use Pod::Usage;

my $verbose = 0;
my $version = 0;
my $quiet = 0;
my $help = 0;
my $man = 0;
my $force = 0;

GetOptions( 
	'help|?' => \$help,
	'man' => => \$man,
	'verbose+' => \$verbose,
	'version' => \$version,
	'silent' => \$quiet,
	'force' => \$force,
	'quiet' => \$quiet
) || pod2usage( 2 );
EPrints::Utils::cmd_version( "create_tables" ) if $version;
pod2usage( 1 ) if $help;
pod2usage( -exitstatus => 0, -verbose => 2 ) if $man;
pod2usage( 2 ) if( scalar @ARGV != 1 ); 

my $noise = 1;
$noise = 0 if( $quiet );
$noise = 1+$verbose if( $verbose );

# Set STDOUT to auto flush (without needing a \n)
$|=1;

my $session = new EPrints::Session( 1, $ARGV[0], $noise, 1 );
exit( 1 ) unless defined $session;

if( $session->get_database->has_table( "archive" ) )
{
	print STDERR "WARNING: Database is NOT empty. Contains an \"archive\" table.\n";
	if( !$force )
	{
		EPrints::Config::abort( "Cannot create tables. Erase the database (erase_archive does this)\nor use --force to override this check." );
	}
}
foreach my $dsid ( &EPrints::DataSet::get_sql_dataset_ids )
{
	my $ds = $session->get_repository->get_dataset( $dsid );
	my $indexes = $ds->count_indexes;
	if( $indexes > 32 )
	{
		print STDERR "WARNING: Main table of dataset \"$dsid\" requires $indexes indexes.\n";
		if( !$force )
		{
			EPrints::Config::abort( "MySQL has a maximum of 32 indexes per table and the '$dsid'\ntable requires $indexes. Add an sql_index=>0 parameter to some of the\nfields in this dataset. See the Documentation for more information\non the 'sql_index' metadata parameter. --force will override this\ncheck but the SQL will probably fail anyway." );
		}
	}
}
		


if( $noise>=1 ) { print "Creating database tables...\n"; }
if( $session->get_database->create_archive_tables() )
{
	if( $noise>=1 ) { print "...done creating database tables.\n"; }
}
else
{
	my $error = $session->get_database->error;
	print STDERR "DB Error: $error\n";
}

if( $noise>=1 ) { print "You should really run import_subjects now.\n"; }

$session->terminate();
exit;


