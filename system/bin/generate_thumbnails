#!/usr/bin/perl -w -I/opt/eprints3/perl_lib 

######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

=pod

=head1 NAME

B<generate_thumbnails> - Generate eprint thumbnails

=head1 SYNOPSIS

B<generate_thumbnails> I<repository_id> [B<options>] [B<eprint ids>]

=head1 DESCRIPTION

=head1 ARGUMENTS

=over 8

=item B<repository_id> 

The ID of the eprint repository to use.

=back

=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exit.

=item B<--man>

Print the full manual page and then exit.

=item B<--quiet>

Be vewwy vewwy quiet. This option will supress all output unless an error occurs.

=item B<--verbose>

Explain in detail what is going on.
May be repeated for greater effect.

=item B<--version>

Output version information and exit.

=back   

__GENERICPOD__

=cut


use EPrints;

use File::Copy;
use strict;
use Getopt::Long;
use Pod::Usage;

my $version = 0;
my $verbose = 0;
my $quiet = 0;
my $help = 0;
my $man = 0;
my $force = 0;

GetOptions( 
	'help|?' => \$help,
	'man' => \$man,
	'version' => \$version,
	'verbose+' => \$verbose,
	'silent' => \$quiet,
	'quiet' => \$quiet,
	'force' => \$force,
) || pod2usage( 2 );
EPrints::Utils::cmd_version( "generate_thumbnails" ) if $version;
pod2usage( 1 ) if $help;
pod2usage( -exitstatus => 0, -verbose => 2 ) if $man;
pod2usage( 2 ) if( scalar @ARGV < 1 ); 

my $noise = 1;
$noise = 0 if( $quiet );
$noise = 1+$verbose if( $verbose );

# Set STDOUT to auto flush (without needing a \n)
$|=1;

my @IDS = @ARGV[1..$#ARGV];


my $session = new EPrints::Session( 1 , $ARGV[0] , $noise );
exit( 1 ) unless( defined $session );

my $dataset = $session->get_repository->get_dataset( "eprint" );

$dataset->map($session, \&generate_thumbnails);

$session->terminate;

sub generate_thumbnails
{
	my( $session, $dataset, $eprint ) = @_;

	return if( scalar(@IDS) and not grep({ $eprint->get_id eq $_ } @IDS) );

	my @finfo = ();
	foreach my $doc ($eprint->get_all_documents)
	{
		$doc->make_thumbnails();
		push @finfo, $doc->icon_url.";".$doc->get_url;
	}
	$eprint->set_value( "fileinfo", join( "|", @finfo ) );
	$eprint->commit;

	print STDERR $eprint->get_id()."\n" if $verbose;
}
