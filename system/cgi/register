######################################################################
#
#  EPrints Register Account 
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;
use strict;

my $handle = new EPrints::Handle;
exit( 0 ) unless( defined $handle );

my( $page, $title ) = make_page( $handle );

$handle->build_page( $title, $page, "register" );
$handle->send_page();

$handle->terminate();

sub make_page
{
	my( $handle ) = @_;

	my $min = 0;
	my $signup_style = $handle->get_repository->get_conf( "signup_style" );
	if( defined $signup_style && $signup_style eq "minimal" )
	{
		$min = 1;
	}

	unless( $handle->get_repository->get_conf("allow_web_signup") )
	{
		return mk_err_page( 
			$handle,
			"cgi/register:no_web_signup" );
	}

	my $user_ds = $handle->get_repository->get_dataset( "user" );

	my $extrafields = $handle->get_repository->get_conf( "extra_reg_fields" );

	my $default_type = $handle->get_repository->get_conf( "default_user_type" ); 
	my $extrafields =  $handle->get_repository->get_conf( "user_registration_fields" ) if !$min;
	$extrafields = [] if( !defined $extrafields );

	my $fieldlist = []; 
	my( $f ) = {};
	foreach my $fieldid ( @{$extrafields} )
	{
		$f->{$fieldid} = $user_ds->get_field( $fieldid )->clone();
		push @{$fieldlist}, $f->{$fieldid};
	}

	my @sysfields;
	unless( $min )
	{
		@sysfields = ( "email", "username", "newpassword" );
	}
	else
	{
		@sysfields = ( "email", "newpassword" );
	}

	foreach my $fieldid ( @sysfields )
	{
		$f->{$fieldid} = $user_ds->get_field( $fieldid )->clone();
		$f->{$fieldid}->set_property( "confid" , "register" );
		$f->{$fieldid}->set_property( "required", 1 );
		push @{$fieldlist}, $f->{$fieldid};
	}

	if( !$handle->have_parameters() )
	{
		my $page = $handle->make_doc_fragment;
		unless( $min )
		{
			$page->appendChild( $handle->html_phrase( "cgi/register:intro" ) );
		}
		else
		{
			$page->appendChild( $handle->html_phrase( "cgi/register:intro_minimal" ) );
		}
		my $defaults = { lang => $handle->get_langid() };
		$page->appendChild( make_reg_form( $handle, $fieldlist, $defaults ));
		
		my $title = $handle->html_phrase( "cgi/register:title" );
		return( $page, $title );
	}



	if( $handle->have_parameters() )
	{
	}
	# Process the form.
	my $v = { lang => $handle->get_langid() };
	foreach my $field ( @{$fieldlist} )
	{
		$v->{$field->get_name} = $field->form_value( $handle );
	}
	$v->{username} = $v->{email} if $min;
	foreach my $field ( @{$fieldlist} )
	{
		if( !EPrints::Utils::is_set( $v->{$field->get_name} ) 
			&& $field->get_property( "required" ) )
		{
			return mk_err_page( 
				$handle,
				"cgi/register:missing_field",
				$fieldlist,
				$v,
				{fieldname=>$field->render_name($handle)} );
		}

		my @problems = $field->validate( $handle, $v->{$field->get_name} );
		if( scalar @problems )
		{
			my $ul = $handle->make_element( "ul" );
			for(@problems)
			{
				my $li = $handle->make_element( "li" );
				$ul->appendChild( $li );
				$li->appendChild( $_ );
			}
			return mk_err_page( 
				$handle,
				"cgi/register:field_error",
				$fieldlist,
				$v,
				{fieldname=>$field->render_name($handle), problems=>$ul} );
		}
	}

	if( defined EPrints::DataObj::User::user_with_email( $handle, $v->{email} ) )
	{
		return mk_err_page( 
			$handle,
			"cgi/register:email_exists", 
			$fieldlist,
			$v,
			{email=>$handle->make_text( $v->{email} )} );
	}

	if( $handle->get_repository->can_call( "check_registration_email" ) )
	{
		unless( $handle->get_repository->call( "check_registration_email", $handle, $v->{email} ) )
		{
			return mk_err_page(
				$handle,
				"cgi/register:email_denied",
				$fieldlist,
				$v,
				{email=>$handle->make_text( $v->{email} )} );
		}
	} 

	if( defined EPrints::DataObj::User::user_with_username( $handle, $v->{username} ) )
	{
		return mk_err_page( 
			$handle,
			"cgi/register:username_exists", 
			$fieldlist,
			$v,
			{username=>$handle->make_text( $v->{username} )} );
	}

	my $pin = sprintf( "%04X%04X%04X%04X",int rand 0xffff,int rand 0xffff,int rand 0xffff,int rand 0xffff );
	my $user_data = { 
		usertype => $default_type,
		newemail => undef,
		pin => $pin,
		pinsettime => time(),
	};
	foreach my $fieldid ( keys %{$f} )
	{
		$user_data->{$fieldid} = $v->{$fieldid};
	}
	if( $min )
	{
		$user_data->{username} = $v->{email};
	}
	my $user_dataset = $handle->get_repository->get_dataset( "user" );
	my $user = $user_dataset->create_object( $handle, $user_data );

	my $page = $handle->make_doc_fragment;
	$page->appendChild( $handle->html_phrase( 
		"cgi/register:created_new_user",
			email=>$handle->make_text( $v->{email} ),
			username=>$handle->make_text( $v->{username} ) ) );

	my $maxdelta = EPrints::Time::human_delay(
		$handle->get_repository->get_conf( "pin_timeout" ) );

	# If email fails then we should abort
	my $rc = $user->mail( 
		"cgi/register:account",
		$handle->html_phrase( 
			"mail_password_pin", 
			confirmurl => $handle->render_link( $handle->get_repository->get_conf( "perl_url" )."/confirm?userid=".$user->get_value( "userid" )."&pin=".$pin ),
			username => $user->render_value( "username" ),
			maxdelta => $handle->make_text( $maxdelta ) ) );

	if( !$rc )
	{
		# couldn't send email, so remove the user object again
		# and apologise
		$user->remove();
		return mk_err_page(
			$handle,
			"general:email_failed",
			$fieldlist,
			$v,
			{} );
	}

#`	$page->appendChild( $handle->html_phrase( "general:frontpage_link" ) );

	my $title = $handle->html_phrase( "cgi/register:title" );
	return( $page, $title );
}


sub mk_err_page
{
	my( $handle, $phrase, $fieldlist, $defaults, $opts ) = @_;

	my $page = $handle->make_doc_fragment;

	$page->appendChild( $handle->render_message( "error", 
		$handle->html_phrase( $phrase, %{$opts} ) ) );

#	$page->appendChild( $handle->html_phrase( "general:frontpage_link" ) );
	if( defined $fieldlist )
	{
		#$page->appendChild( $handle->render_ruler );
		$page->appendChild( make_reg_form( $handle, $fieldlist, $defaults ) );
	}

	return( 
		$page,	
		$handle->html_phrase( "cgi/register:error" ) );
}

sub make_reg_form
{
	my( $handle, $fieldlist, $defaults ) = @_;

	$defaults->{newpassword} = undef;

	return $handle->render_input_form(
			fields=>$fieldlist,
			values=>$defaults,
			show_help=>1,
			show_names=>1,
			comments=>{above_buttons=>$handle->html_phrase( "cgi/register:register_agreement" )},
			default_action=>"submit",
			buttons=>{
				submit=>$handle->phrase( "cgi/register:action_submit" )
			},
			dest=>"register" );
}
