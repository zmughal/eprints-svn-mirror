######################################################################
#
#  EPrints Set Password 
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;
use strict;

my $handle = new EPrints::Handle;
exit( 0 ) unless( defined $handle );

my( $page, $title ) = make_page( $handle );

# $page->appendChild( $handle->html_phrase( "general:frontpage_link" ) );

$handle->build_page( $title, $page, "reset_password" );
$handle->send_page();

$handle->terminate();

sub make_page
{
	my( $handle ) = @_;

	my $page = $handle->make_element( "div", class => "ep_block");
	my $table = $handle->make_element( "table" );
	my $tr = $handle->make_element( "tr" );
	my $td = $handle->make_element( "td" );
	$page->appendChild( $table );
	$table->appendChild( $tr );
	$tr->appendChild( $td );

	my $title = $handle->html_phrase( "cgi/set_password:password_changeonlytitle" );

	my $reset_ok =  $handle->get_repository->get_conf("allow_reset_password");
	if( !$reset_ok )
	{
		$td->appendChild( $handle->html_phrase( "cgi/set_password:disabled" ) );
		$td->appendChild( $handle->html_phrase( "general:frontpage_link" ) );
		return( $page, $title );
	}

	my $user_ds = $handle->get_repository->get_dataset( "user" );

	my $f_email = $user_ds->get_field( "email" )->clone();
	$f_email->set_property( "confid" , "setpass" );

	my $f_newpass = $user_ds->get_field( "newpassword" )->clone();
	$f_newpass->set_property( "confid" , "setpass" );

	if( ! $handle->have_parameters() )
	{
		$td->appendChild( $handle->html_phrase( "cgi/set_password:pchange" ) );

		my $fields = [ $f_email, $f_newpass ];

		$td->appendChild( $handle->render_input_form(
			fields=>$fields,
			values=>{
				lang => $handle->get_langid()
			},
			show_help=>1,
			default_action=>"submit",
			buttons=>{
				submit=>$handle->phrase( "cgi/set_password:action_submit" )
			},
			dest=>"reset_password" ) );

		return( $page, $title );
	}

	# Process the form.
	my $email = $f_email->form_value( $handle );
	my $lang = $handle->get_langid();
	my $newpassword = $f_newpass->form_value( $handle );

	my $user = EPrints::DataObj::User::user_with_email( $handle, $email );

	if( ! EPrints::Utils::is_set( $email ) )
	{
		$page->appendChild( $handle->html_phrase( 
			"cgi/set_password:no_email" ) );
		return( 
			$page, 
			$handle->html_phrase( 
				"cgi/set_password:error_title" ) );
	}

	if( !defined $user )
	{
		$page->appendChild( $handle->html_phrase( 
			"cgi/set_password:no_such_user",
			email=>$handle->make_text( $email ) ) );
		return( 
			$page, 
			$handle->html_phrase( 
				"cgi/set_password:error_title" ) );
	}

	if( !$user->allow( "set-password" ) )
	{
		$page->appendChild( $handle->html_phrase( 
			"cgi/set_password:no_priv",
			email=>$handle->make_text( $email ) ) );
		return( 
			$page, 
			$handle->html_phrase( 
				"cgi/set_password:error_title" ) );
	}

	if( !defined $newpassword || $newpassword eq "" )
	{
		$page->appendChild( $handle->html_phrase( 
			"cgi/set_password:no_password" ) );
		return( 
			$page, 
			$handle->html_phrase( 
				"cgi/set_password:error_title" ) );
	}

	$user->set_value( "newpassword", $newpassword );
	my $pin = sprintf( "%04X%04X%04X%04X",int rand 0xffff,int rand 0xffff,int rand 0xffff,int rand 0xffff );
	$user->set_value( "newemail", undef );
	$user->set_value( "pin", $pin );
	$user->set_value( "pinsettime", time() );
	$user->commit();
	
	my $maxdelta = EPrints::Time::human_delay(
		$handle->get_repository->get_conf( "pin_timeout" ) );
	
	my $rc = $user->mail( 
		"cgi/set_password:account",
		$handle->html_phrase( 
			"mail_password_pin", 
			confirmurl => $handle->render_link( $handle->get_repository->get_conf( "perl_url" )."/confirm?userid=".$user->get_value( "userid" )."&pin=".$pin ),
			username => $user->render_value( "username" ),
			maxdelta => $handle->make_text( $maxdelta ) ) );

	# did email send OK?
	if( !$rc )
	{
		$page->appendChild( $handle->html_phrase( 
			"general:email_failed" ) );
		return( 
			$page, 
			$handle->html_phrase( 
				"cgi/set_password:error_title" ) );
	}


	$page->appendChild( $handle->html_phrase( 
		"cgi/set_password:mail_sent",
		email=>$handle->make_text( $email ),
		maxdelta => $handle->make_text( $maxdelta ) ) );
	return( $page, $title );
}

