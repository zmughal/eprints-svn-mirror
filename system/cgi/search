######################################################################
#
#  EPrints Simple Search Form
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;

use strict;
use warnings;
my $repo = EPrints->new->current_repository;
exit( 0 ) unless( defined $repo );

my $path_info = $repo->get_request->path_info;
# lose a leading slash
$path_info =~ s#^/##;

my $args = $repo->get_request->args;
$args = "" if !defined $args;
$args = "?$args" if length( $args );

my( $datasetid, $searchid ) = split /\//, $path_info;

if( !defined $datasetid )
{
	$repo->redirect( $repo->config( "http_cgiroot" )."/search/simple$args" );
	exit;
}

# cover the old systems sins...
if( $datasetid eq "advsearch" )
{
	$repo->redirect( $repo->config( "http_cgiroot" )."/search/advanced$args" );
	exit;
}

if( defined($repo->param( "dataset" )) )
{
	if( !$searchid )
	{
		$repo->redirect( $repo->config( "http_cgiroot" )."/search/".$repo->param( "dataset" )."/$datasetid$args" );
		exit;
	}
	# handle the user changing the search dataset after a search
	elsif( $repo->param( "dataset" ) ne $datasetid )
	{
		$repo->redirect( $repo->config( "http_cgiroot" )."/search/".$repo->param( "dataset" )."/$searchid$args" );
		exit;
	}
}

if( $datasetid =~ /^(advanced)|(simple)$/ )
{
	$searchid = $datasetid;
	$datasetid = "archive";
}
elsif( !defined $searchid )
{
	$repo->redirect( $repo->config( "http_cgiroot" )."/search/$datasetid/simple$args" );
	exit;
}

my $dataset = $repo->dataset( $datasetid );
if( !defined $dataset )
{
	$repo->not_found;
	exit;
}

my $sconf;
if( $datasetid eq "archive" )
{
	$sconf = $repo->config( "search", $searchid );
}
else
{
	$sconf = $repo->config( "datasets", $datasetid, "search", $searchid );
}
if( !defined $sconf )
{
	$sconf = {
		search_fields => [],
		order_methods => {
			byid => $dataset->get_key_field->name,
		},
		default_order => "byid",
		citation => "result",
		show_zero_results => 1,
	};
	if( $searchid eq "simple" )
	{
		$sconf->{search_fields} = [{
			id => "q",
			meta_fields => [
				map { $_->name }
				grep { !$_->is_virtual && $_->property( "export_as_xml" ) }
				$dataset->fields
			]
		}];
	}
	elsif( $searchid eq "advanced" )
	{
		$sconf->{search_fields} = [
			map { { meta_fields => [$_->name] } }
			grep { !$_->is_virtual && $_->property( "export_as_xml" ) }
			$dataset->fields
		];
	}
}
foreach my $sfs (@{$sconf->{search_fields}})
{
	for(@{$sfs->{meta_fields}})
	{
		$_ = "documents" if $_ eq "_fulltext_";
	}
}

EPrints::ScreenProcessor->process( 
	session => $repo, 
	url => $repo->config( "http_cgiroot" )."/search/$datasetid/$searchid",
	sconf => $sconf,
	template => $sconf->{template},
	screenid => "Search",
	searchid => $searchid,
	dataset => $dataset,
);

exit;
