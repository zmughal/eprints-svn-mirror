######################################################################
#
#  EPrints Toolbox
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;
use EPrints::Toolbox;
use strict;

my $handle = new EPrints::Handle;
exit( 0 ) unless( defined $handle );

my $cmd = $handle->param( "verb" );
my $username = $handle->param( "username" );
my $password = $handle->param( "password" );

my %opts = ();

if( !$handle->valid_login( $username, $password ) )
{
	toolbox_fail( $handle, "Invalid username/password" );
}

$handle->{current_user} = EPrints::DataObj::User::user_with_username( $handle, $username );

if( !$handle->{current_user}->allow( "toolbox" ) )
{
	toolbox_fail( $handle, "User '$username' not authorised to use toolbox functions" );
}

$opts{data_fn} = sub {
	my( %opts ) = @_;

	my $data = $handle->param( "data" );
	if( EPrints::Utils::is_set( $data ) )
	{
		return $data;
	}

	my $fn = $opts{handle}->param( "datafile" );
	$data = join( "", <$fn> );
	return $data;
};


$opts{format} = $handle->param( "format" );
$opts{filename} = $handle->param( "filename" );
$opts{field} = $handle->param( "field" );

my $eprintid = $handle->param( "eprint" );
if( EPrints::Utils::is_set( $eprintid ) )
{
	$opts{eprint} = EPrints::DataObj::EPrint->new( $handle, $eprintid );
	if( !defined $opts{eprint} )
	{
		toolbox_fail( $handle, "Could not find eprint $eprintid" );
	}
}

my $docid = $handle->param( "document" );
if( EPrints::Utils::is_set( $docid ) )
{
	$opts{document} = EPrints::DataObj::Document->new( $handle, $docid );
	if( !defined $opts{document} )
	{
		toolbox_fail( $handle, "Could not find document $docid" );
	}
}

$opts{handle} = $handle;

my $fn_name = "EPrints::Toolbox::tool_".$cmd;

my( $rc, $output ) = eval "$fn_name( \%opts )";
if( $@ )
{
	toolbox_fail( $handle, "Command '$cmd' failed.", [$@] );
}
if( $rc ) 
{
	toolbox_fail( $handle, "Command Failed", $output );
}

$handle->send_http_header( content_type=>"text/plain" );
print $output;

$handle->terminate();
exit;

sub toolbox_fail
{
	my( $handle, $msg, $problems ) = @_;

	my $error = $handle->make_element( "eprints_toolbox_error" );
	my $message = $handle->make_element( "message" );
	$error->appendChild( $message );
	$message->appendChild( $handle->make_text( $msg ) );
	if( defined $problems )
	{
		foreach my $problem ( @{$problems} )
		{
			my $problem_dom = $handle->make_element( "problem" );
			$error->appendChild( $problem_dom );
			$problem_dom->appendChild( $handle->make_text( $problem ) );
		}
	}
	EPrints::XML::tidy( $error );
	$handle->send_http_header( content_type=>"text/xml" );

	print EPrints::XML::to_string( $error );
	$handle->terminate;
	exit;
}

