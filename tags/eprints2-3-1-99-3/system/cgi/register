######################################################################
#
#  EPrints Register Account 
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints::Session;
use strict;

my $session = new EPrints::Session;
Apache::exit( 0 ) unless( defined $session );

my( $page, $title ) = make_page( $session );

$session->build_page( $title, $page, "register" );
$session->send_page();

$session->terminate();

sub make_page
{
	my( $session ) = @_;

	unless( $session->get_archive()->get_conf("allow_web_signup") )
	{
		return mk_err_page( 
			$session,
			"cgi/register:no_web_signup" );
	}

	my $mlangopt = $session->get_archive()->get_conf("multi_language_options");

	my $user_ds = $session->get_archive()->get_dataset( "user" );

	my $extrafields = $session->get_archive()->get_conf( "extra_reg_fields" );

	my $default_type = $session->get_archive()->get_conf( "default_user_type" ); 
	my $extrafields =  $session->get_archive()->get_conf( "user_registration_fields" );
	$extrafields = [] if( !defined $extrafields );

	my $fieldlist = []; 
	my( $f ) = {};
	foreach my $fieldid ( @{$extrafields} )
	{
		$f->{$fieldid} = $user_ds->get_field( $fieldid )->clone();
		if( $user_ds->field_required_in_type( $f->{$fieldid}, $default_type ))
		{
			$f->{$fieldid}->set_property( "required", 1 );
		}
		push @{$fieldlist}, $f->{$fieldid};
	}

	my @sysfields = ( "email", "username", "newpassword" );
	if( $mlangopt ) { push @sysfields,'lang'; }

	foreach my $fieldid ( @sysfields )
	{
		$f->{$fieldid} = $user_ds->get_field( $fieldid )->clone();
		$f->{$fieldid}->set_property( "confid" , "register" );
		$f->{$fieldid}->set_property( "required", 1 );
		push @{$fieldlist}, $f->{$fieldid};
	}

	if( !$session->have_parameters() )
	{
		my $page = $session->make_doc_fragment;
		$page->appendChild( $session->html_phrase( "cgi/register:intro" ) );
		my $defaults = { lang => $session->get_langid() };
		$page->appendChild( make_reg_form( $session, $fieldlist, $defaults ));
		
		my $title = $session->html_phrase( "cgi/register:title" );
		return( $page, $title );
	}



	if( $session->have_parameters() )
	{
	}
	# Process the form.
	my $v = { lang => $session->get_langid() };
	foreach my $field ( @{$fieldlist} )
	{
		$v->{$field->get_name} = $field->form_value( $session );
	}
	foreach my $field ( @{$fieldlist} )
	{
		if( !EPrints::Utils::is_set( $v->{$field->get_name} ) 
			&& $field->get_property( "required" ) )
		{
			return mk_err_page( 
				$session,
				"cgi/register:missing_field",
				$fieldlist,
				$v,
				{fieldname=>$field->render_name($session)} );
		}
	}

	if( defined EPrints::User::user_with_email( $session, $v->{email} ) )
	{
		return mk_err_page( 
			$session,
			"cgi/register:email_exists", 
			$fieldlist,
			$v,
			{email=>$session->make_text( $v->{email} )} );
	}

	if( defined EPrints::User::user_with_username( $session, $v->{username} ) )
	{
		return mk_err_page( 
			$session,
			"cgi/register:username_exists", 
			$fieldlist,
			$v,
			{username=>$session->make_text( $v->{username} )} );
	}

	my $user = EPrints::User::create_user( $session, $default_type );
	foreach my $fieldid ( keys %{$f} )
	{
		$user->set_value( $fieldid, $v->{$fieldid} );
	}

	my $page = $session->make_doc_fragment;
	$page->appendChild( $session->html_phrase( 
		"cgi/register:created_new_user",
			email=>$session->make_text( $v->{email} ),
			username=>$session->make_text( $v->{username} ) ) );

	my $pin = sprintf( "%04X%04X%04X%04X",int rand 0xffff,int rand 0xffff,int rand 0xffff,int rand 0xffff );
	$user->set_value( "newemail", undef );
	$user->set_value( "pin", $pin );
	$user->set_value( "pinsettime", time() );
	$user->commit();
	$user->mail( 
		"cgi/register:account",
		$session->html_phrase( 
			"mail_password_pin", 
			confirmurl => $session->make_text( $session->get_archive()->get_conf( "perl_url" )."/confirm?userid=".$user->get_value( "userid" )."&pin=".$pin ),
			username => $user->render_value( "username" ) ) );

#`	$page->appendChild( $session->html_phrase( "general:frontpage_link" ) );

	my $title = $session->html_phrase( "cgi/register:title" );
	return( $page, $title );
}


sub mk_err_page
{
	my( $session, $phrase, $fieldlist, $defaults, $opts ) = @_;

	my $page = $session->make_doc_fragment;
	$page->appendChild( $session->html_phrase( $phrase, %{$opts} ) );
#	$page->appendChild( $session->html_phrase( "general:frontpage_link" ) );
	if( defined $fieldlist )
	{
		$page->appendChild( $session->render_ruler );
		$page->appendChild( make_reg_form( $session, $fieldlist, $defaults ) );
	}

	return( 
		$page,	
		$session->html_phrase( "cgi/register:error" ) );
}

sub make_reg_form
{
	my( $session, $fieldlist, $defaults ) = @_;

	$defaults->{newpassword} = undef;

	return $session->render_input_form(
			fields=>$fieldlist,
			values=>$defaults,
			show_help=>1,
			show_names=>1,
			comments=>{above_buttons=>$session->html_phrase( "cgi/register:register_agreement" )},
			default_action=>"submit",
			buttons=>{
				submit=>$session->phrase( "cgi/register:action_submit" )
			},
			dest=>"register" );
}
