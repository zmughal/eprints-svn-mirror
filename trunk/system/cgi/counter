######################################################################
#
#  Show the number of eprints currently in the repository
#
#  Used for remote monitoring of repository growth. eg. by 
#  software.eprints.org
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;


use strict;

my $eprints = EPrints->new;
my $repo = $eprints->current_repository;
exit( 0 ) unless( defined $repo );

$repo->send_http_header( content_type=>"text/plain; charset=UTF-8" );

my @ds_ids = $repo->get_sql_dataset_ids();
foreach my $ds_id ( sort @ds_ids )
{
	my $ds = $repo->dataset( $ds_id );
	my $table = $ds->get_sql_table_name;
	print $ds_id.": ".$repo->get_database->count_table( $table )."\n";
}
{
	my $ds = $repo->dataset( "eprint" );
	my $search = $ds->prepare_search;
	my( $ds_ids, $counts ) = $search->perform_groupby( $ds->field( "eprint_status" ) );
	foreach my $i (0..$#$ds_ids)
	{
		print sprintf("%s: %i\n",
			$ds_ids->[$i],
			$counts->[$i],
		);
	}
}
print sprintf("version: %s (%s)\n",
	$repo->config( "version_id" ),
	$repo->config( "version" ) );

# Indexer Status
my $daemon = EPrints::Index::Daemon->new(
		session => $repo,
		logfile => EPrints::Index::logfile(),
		noise => ($repo->{noise}||1),
);

my $status = "not-running";
$status = "running" if $daemon->is_running();
$status = "stalled" if $daemon->has_stalled();
print "indexer: $status\n";

exit;
