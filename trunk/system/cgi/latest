######################################################################
#
#  Show EPrints modified or added in the past 7 days
#
######################################################################
#
#  __COPYRIGHT__
#
# Copyright 2000-2008 University of Southampton. All Rights Reserved.
# 
#  __LICENSE__
#
######################################################################

use EPrints;

use strict;
my $session = new EPrints::Session;
exit( 0 ) unless( defined $session );

my $ds = $session->get_repository->get_dataset( "archive" );
my $page=$session->make_doc_fragment();

$page->appendChild( $session->html_phrase( "cgi/latest:intro" ) );

my $citation = $session->get_repository->get_conf( "latest_citation" );

my $seensome = 0;
for( my $d=0; $d<7; ++$d )
{
	my $t = time-60*60*24*$d;
	my $searchexp = new EPrints::Search(
		filters => [
			{ meta_fields=>[ 'metadata_visibility' ], value=>'show', match=>'EX', describe=>0 },
		],
		session => $session,
		dataset => $ds );
	

	my $date = EPrints::Utils::get_iso_timestamp( $t );
	$searchexp->add_field( $ds->get_field( "datestamp" ), $date );
		
	$searchexp->perform_search();

	if( $searchexp->count() )
	{
		$seensome = 1;

		my $day;
		if( $d == 0 )
		{
			$day = $session->html_phrase( "cgi/latest:today" );
		}
		elsif( $d == 1 )
		{
			$day = $session->html_phrase( "cgi/latest:yesterday" );
		}
		else
		{
			my $dow = (localtime($t))[6];
			$day = $session->html_phrase( "cgi/latest:day_".$dow );
		}

		my $h2= $session->make_element( "h2" );
		$h2->appendChild( $day );
		$page->appendChild( $h2 );
	
		$searchexp->map( sub{ 
			my( $session, $dataset, $item, $info ) = @_;
		
			my $p = $session->make_element( "p" );
			$p->appendChild( $item->render_citation_link( $citation ) );
			$page->appendChild( $p );
		} );
	
		$page->appendChild( $session->render_ruler() );
	}
	$searchexp->dispose();
}
unless( $seensome )
{
	$page->appendChild( $session->html_phrase( "cgi/latest:none" ) );
}

$page->appendChild( $session->html_phrase( "general:frontpage_link" ) );
	
my $title = $session->html_phrase( "cgi/latest:title" );
$session->build_page( $title, $page, "latest" );
$session->send_page();

$session->terminate;
